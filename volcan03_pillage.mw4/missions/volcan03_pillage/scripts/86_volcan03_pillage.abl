
//*********************************************************************************

fsm Pillage : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var

		eternal integer     playermech;
			
        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
        static integer      checkrange;         // How far from the Nav Point you have to be                                            
                                                //
        eternal boolean     obj1;               // This indicates whether Obj 1 was completed
        eternal boolean     obj2;               // This indicates whether Obj 2 was completed
        eternal boolean     obj3;               // This indicates whether Obj 3 was completed
        eternal boolean     obj4;               // This indicates whether Obj 4 was completed
        eternal boolean     obj5;               // This indicates whether Obj 5 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        static boolean      obj2revealed;       // This indicates that Obj 2 has been revealed
        static boolean      obj3revealed;       // This indicates that Obj 3 has been revealed
        static boolean      obj4revealed;       // This indicates that Obj 4 has been revealed
        //    
		
		static integer	    cinema_cut;
 
        static boolean      boom1;              // The explosions tied to crates
        static boolean      boom2;              // The explosions tied to crates
        static boolean      boom3;              // The explosions tied to crates
		eternal boolean     BaseCleared;        // The base is cleared of enemies
        eternal integer     TaskSquad;          // The TaskSquad is ready to rock and roll
        eternal boolean     EnemySalvage;       // The enemy has taken over the salvage Mechs
        static locpoint     spot;               // Where the culturals teleport to
        eternal integer     retaldelay;         // How long it takes the retaliation to strike back
        static boolean      OurMechs;           // We got the salvage, not the bad guys
        static integer      LanceNum;           // The number of lancemates you brought with you
        
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

		    playermech = getplayervehicle(epl_player0);	

            if (GetLancemate(1)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(1));
                LanceNum = 1;
            endif;

            if (GetLancemate(2)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(2));
                LanceNum = LanceNum + 1;
            endif;

            if (GetLancemate(3)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(3));
                LanceNum = LanceNum + 1;
            endif;
 	
			// Salvage Team
            GroupAddObject(GroupObjectId(10),eve_salvage01);
            GroupAddObject(GroupObjectId(10),eve_salvage02);
            GroupAddObject(GroupObjectId(10),eve_salvage03);
			SetGroupAI(groupobjectid(10),GROUPAI_MOODSQUAD);
            
            // MechWorks Interceptors
            GroupAddObject(GroupObjectId(20),eve_intercept_01);
            GroupAddObject(GroupObjectId(20),eve_intercept_02);
            GroupAddObject(GroupObjectId(20),eve_intercept_03);
            GroupAddObject(GroupObjectId(20),eve_intercept_04);
			SetGroupAI(groupobjectid(20),GROUPAI_MOODSQUAD);
		    
            // MechWorks Turret defense
            GroupAddObject(GroupObjectId(21),etu_turret1);
            GroupAddObject(GroupObjectId(21),etu_turret2);
            GroupAddObject(GroupObjectId(21),etu_turret3);
            GroupAddObject(GroupObjectId(21),etu_turret4);
            SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);
            
            // MechWorks Retaliation strike
            GroupAddObject(GroupObjectId(22),eve_retal_01);
            GroupAddObject(GroupObjectId(22),eve_retal_02);
            GroupAddObject(GroupObjectId(22),eve_retal_03);
            GroupAddObject(GroupObjectId(22),eve_retal_04);
            GroupAddObject(GroupObjectId(22),eve_retal_05);
            GroupAddObject(GroupObjectId(22),eve_retal_06);
            SetGroupAI(groupobjectid(22),GROUPAI_MOODSQUAD);
            
            cinema_cut = 0; 						

    		SetTarget(ede_people_desert_01,eve_salvage01);
            SetTarget(ede_people_desert_02,eve_salvage01);
            SetTarget(ede_people_desert_03,eve_salvage02);
            SetTarget(ede_people_desert_04,eve_salvage03);
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
			
   	    //playsound(eso_start);
		setactivationdistance(1200);

        setradarnav(ena_Nav_Alpha,1);

        revealnavpoint(ena_Nav_Alpha);
        setnavpoint(ena_Nav_Alpha);

        revealobjective(eob_objective1);
        
        settargetdesirability(eve_defender_01,-1);
        setsensorvisibility(eve_defender_01,false);
        
        checkrange      = 150;
        retaldelay      = 80;       // Used to be 20
        TaskSquad       = 0;

        StartMusic("Music\Daggers",3.0,3.5,50);

	    trans opening;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

    if IsMusicPlaying == false then
    	StartMusic("music\LavaAmbLoop",0.0,3.0,40);
    endif;
    
// Triggers

        if (not basecleared) then
            if (GroupAllDead(GroupObjectID(20))) then
                if (GroupNumDead(GroupObjectID(21))>2) then
                    basecleared=true;
                    revealobjective(eob_objective3);
                else
                    if (isdead(ebu_base_turret_tower)) then
                        basecleared=true;
                        revealobjective(eob_objective3);
                    endif;
                endif;
            endif;
        endif;
        
        if (TaskSquad==1) then  // The Helo is doing its land order
            TaskSquad=10;
            starttimer(10);
            PlayVoiceOverOnce("VOX\OP5\MI3\Closeone",talker_HEL);
        endif;
        
        if (TaskSquad==10) then // The Helo should be on the ground
            if (timegreater(10,8)) then
                TaskSquad=2;
                GetLocation(ena_chopperdrop,spot);
                teleport(ede_people_desert_01,spot);
                teleport(ede_people_desert_02,spot);
                teleport(ede_people_desert_03,spot);
                teleport(ede_people_desert_04,spot);
            endif;
        endif;
        
        if (not GetGlobalTrigger(13)) then
            if (timegreater(10,27)) then
                PlayVoiceOverOnce("VOX\OP5\MI3\Toys",talker_HEL);
                SetGlobalTrigger(13,true);
            endif;
        endif;
        
        if (not enemysalvage) then
            if (TaskSquad==2) then
                    if (timegreater(10,45)) then
                        if (GroupAllDead(GroupObjectID(10))) then
                            killtimer(10);
                            TaskSquad=3;
                        else
                            killtimer(10);
                            PlayVoiceOverOnce("VOX\OP5\MI3\Friendlies",talker_BAD);
                            SetAlignment(eve_salvage01,friendly);
                            SetAlignment(eve_salvage02,friendly);
                            SetAlignment(eve_salvage03,friendly);
                            OurMechs = true;
                            TaskSquad=3;
                    endif;
                endif;
            endif;
        endif;

// Exploding crates triggers

        if (not boom1) then
            if (isdead(ebu_boomer01)) then
                destroy(ebu_boomfx01);
                boom1=true;
            endif;    	
        endif;    	

        if (not boom2) then
            if (isdead(ebu_boomer02)) then
                destroy(ebu_boomfx02);
                boom2=true;
            endif;    	
        endif;    	

        if (not boom3) then
            if (isdead(ebu_Ammo_Storage)) then
                destroy(ebu_boomfx03);
                destroy(eve_defender_01);
                PlayVoiceOverOnce("VOX\OP5\MI3\Ammo",talker_ERI);
                PlayVoiceOverOnce("VOX\OP5\MI3\Ammo",talker_BAD);
                PlayLancemateSound("VOX\OP5\MI3\Ammo",1);
                boom3=true;
            endif;    	
        endif;    	

        if (not boom3) then
            if (isdead(ebu_boomfx03)) then
                destroy(ebu_Ammo_Storage);
                destroy(eve_defender_01);
                PlayVoiceOverOnce("VOX\OP5\MI3\Ammo",talker_ERI);
                PlayVoiceOverOnce("VOX\OP5\MI3\Ammo",talker_BAD);
                PlayLancemateSound("VOX\OP5\MI3\Ammo",1);
                boom3=true;
            endif;    	
        endif;    	

// Sound Triggers

    if (not GetGlobalTrigger(1)) then       // Shortly after game starts you get the report from Helo
        if (timegreater(1,20)) then
            SetGlobalTrigger(1,true);
            PlayVoiceOverOnce("VOX\OP5\MI3\Patch",talker_BAD);
            PlayVoiceOverOnce("VOX\OP5\MI3\Patch",talker_HEL);
            PlayVoiceOverOnce("VOX\OP5\MI3\Patch2",talker_BAD);
        endif;
    endif;

    if (not GetGlobalTrigger(2)) then       // Spotting the enemies
        if (iswithin(playermech,GroupObjectID(20),800)) then
            SetGlobalTrigger(2,true);
            PlayLancemateSound("VOX\OP5\MI3\Swarm",1);
        endif;
    endif;

// Obj 1 - Defeat the MechWorks defenders

    if (not obj1) then
        if (groupalldead(GroupObjectID(20))) then
            if (groupalldead(GroupObjectID(21))) then
                obj1=true;
                PlayVoiceOverOnce("VOX\OP5\MI3\Alpha",talker_ERI);
                PlayVoiceOverOnce("VOX\OP5\MI3\Alpha",talker_BAD);
                PlayVoiceOverOnce("VOX\OP5\MI3\Alpha2",talker_ERI);
                successobjective(eob_objective1);
            else
                if (isdead(ebu_base_turret_tower)) then
                    obj1=true;
                    PlayVoiceOverOnce("VOX\OP5\MI3\Alpha",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP5\MI3\Alpha",talker_BAD);
                    PlayVoiceOverOnce("VOX\OP5\MI3\Alpha2",talker_ERI);
                    successobjective(eob_objective1);
                endif;
            endif;
        endif;
    endif;

// Obj 2 - Destroy Barracks before salvage Mechs start up

    if (not obj2revealed) then
        if (iswithin(playermech,ebu_barracks,1100)) then
            obj2revealed=true;
            revealobjective(eob_objective2);
            PlayVoiceOverOnce("VOX\OP5\MI3\Barracks",talker_BAD);
            starttimer(11);
        endif;
    endif;
    
    if (not OurMechs) then
        if (not obj2) then
            if (timegreater(11,240)) then
                if (not GroupAllDead(GroupObjectID(10))) then
                    setalignment(eve_salvage01,enemy);
                    setalignment(eve_salvage02,enemy);
                    setalignment(eve_salvage03,enemy);
                    PlayVoiceOverOnce("VOX\OP5\MI3\Destroyed3",talker_BAD);
                    PlayVoiceOverOnce("VOX\OP5\MI3\Destroyed",talker_ERI);
                    failobjective(eob_objective2);
                    EnemySalvage=true;
                    obj2=true;
                endif;
            endif;
        endif;
    endif;
    
    if (not OurMechs) then
        if (not obj2) then
            if (GroupAllDead(GroupObjectID(10))) then
                hideobjective(eob_objective2);
                obj2=true;
            endif;
        endif;
    endif;
    
    if (not obj2) then
        if (isdead(ebu_barracks)) then
            obj2=true;
            PlayVoiceOverOnce("VOX\OP5\MI3\Destroyed2",talker_BAD);
            successobjective(eob_objective2);
        endif;
    endif;

// Obj 3 - Defend the Transport Chopper

    if (not obj3) then
        //if (TaskSquad==0) then  -- This used to check whether the guys were deployed
            if (isdead(eve_pctech)) then
                KillVoiceOvers;
                failobjective(eob_objective3);
                endmission(false,10);
                PlayVoiceOverOnce("VOX\Generic\VehicleDead",talker_BAD);
                trans lost;
            endif;
        //endif;
    endif;
    
    if (not obj3) then
        if (TaskSquad<>0) then
            if (isdead(eve_pctech)) then
                failobjective(eob_objective3);
                obj3=true;
            endif;
        endif;
    endif;

// Obj 4 - Defeat the Steiner retaliatory strike

    if (obj1) then
        if (not obj4revealed) then
            if (iswithin(GroupObjectID(22),playermech,1300)) then
                obj4revealed=true;
                revealobjective(eob_objective4);
                PlayVoiceOverOnce("VOX\OP5\MI3\Steiner",talker_BAD);
                PlayVoiceOverOnce("VOX\OP5\MI3\Steiner",talker_ERI);
                StartMusic("Music\T3loop",3.0,3.5,50);
            endif;
        endif;    
    endif;
    
    if (not obj4) then
        if (GroupAllDead(GroupObjectID(22))) then
            successobjective(eob_objective4);
            if (not isdead(eve_pctech)) then
                successobjective(eob_objective3);
            endif;
            starttimer(3);
            PlayVoiceOverOnce("VOX\OP5\MI3\Victory",talker_ERI);
            obj4 = true;
        endif;
    endif;
    
    if (timegreater(3,5)) then     // Delay after everyone is dead
      
      // Salvage Added by Ferret 7/28/01         
        AddWeaponSalvage("ClanLBXAC20",1);
        AddWeaponSalvage("ClanUltraAC10",2);
        AddWeaponSalvage("ClanUltraAC20",1);
        AddWeaponSalvage("ClanGauss",2);
        AddWeaponSalvage("Thunderbolt",1);            
        AddWeaponSalvage("LRM20",2);            
        AddWeaponSalvage("MRM10",1);
        AddWeaponSalvage("MRM20",1);

        if (not isdead(eve_salvage01)) then
            AddMechInstanceSalvage("Daishi");
        endif;
        
        if (not isdead(eve_salvage02)) then
            AddMechInstanceSalvage("Atlas");
        endif;

        if (not isdead(eve_salvage03)) then
            AddMechInstanceSalvage("Mad Cat MKII");
        endif;
      //
        
        trans won;
    endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\EriDestroyedKnight2",talker_BAD);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
		cinema_cut = cinema_cut + 1;
        trans lost;
	endif;

// Betty speaks when reaching Nav Point

    if (not alphareached) then
	    if (isnavrevealed(ena_nav_alpha)) then
		    if (iswithin(playermech,ena_nav_alpha,checkrange)) then
			    playvoiceover("VO\generic\navalpha",talker_Bet);
			    alphareached = true;
//                setnavpoint(ena_nav_beta);
		    endif;
	    endif;
    endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;

	code

    	if (cinemaskip) then
        	KillVoiceOvers;
    		cinema_cut = 0;
    		KillTimer(gti_timer_21);
    		SetGlobalTrigger(0,true);
    		playerai(epl_player0,false);
            StartTimer(1);      // This is the mission start timer
            ResetTimer(1);
            CinemaEnd;
            SetCompositingEnabled(True);

    		trans sit;
        endif;
        
     	switch (cinema_cut)
    			
            case 0:
                SetActiveCamera(eca_CameraShip);

                PlayVoiceOverOnce("VOX\OP5\MI3\Intro",talker_BAD);
                PlayVoiceOverOnce("VOX\OP5\MI3\Intro",talker_ERI);
                PlayVoiceOverOnce("VOX\OP5\MI3\Surprise",talker_BAD);
                
                playerai(epl_player0,true);
                SetCompositingEnabled(False);
                CinemaStart;
                
                FadeToBlack(0.0);
                starttimer(21);
                resettimer(21);
                
                Targetfollowobject(ede_introcam);
                Camerafollowobject(ede_introcam);
                Targetoffset(75.0,40.0,350.0,0.0,false);
                Targetoffset(-200.0,10.0,-80.0,14.0,false);
                Cameraoffset(0.0,100.0,-200.0,0.0,false);
                Cameraoffset(-40.0,20.0,-160.0,14.0,false);
                FadeFromBlack(3.0);
                
                cinema_cut = cinema_cut + 1;
            endcase;
            
            case 1:
                if (timegreater(21,15)) then
                    PlayVoiceOverOnce("VOX\Generic\Roger3",talker_ERI);
                endif;
                
                if (timegreater(21,17)) then
                	KillVoiceOvers;
            		cinema_cut = 0;
            		KillTimer(gti_timer_21);
            		SetGlobalTrigger(0,true);
            		playerai(epl_player0,false);
                    StartTimer(1);      // This is the mission start timer
                    ResetTimer(1);
                    CinemaEnd;
                    SetCompositingEnabled(True);
                    
            		trans sit;
                endif;
            endcase;
    			
        endswitch;
	
endstate;

state won;
    code
		
		if (cinemaskip) then
			cinema_cut = 20;
			playerai(epl_player0,false);
			//cinemaend;
			KillVoiceOvers;
			Endmission(true,1);
		endif;

		switch cinema_cut
		
			case 0:	
                endmission(true,16);
                StartMusic("Music\Victory",3.0,3.5,50);
                
                PlayVoiceOverOnce("VOX\OP5\MI3\Victory",talker_BAD);
                PlayLancemateSound("VOX\OP5\MI3\Victory",1);
                if (not GroupAllDead(GroupObjectID(1))) then
                    if (not LanceNum == 0) then
                        PlayVoiceOverOnce("VOX\OP5\MI3\Victory2",talker_ERI);
                    endif;
				endif;
                SetCompositingEnabled(False);
                CinemaStart;
				setactivecamera (eca_cameraship);
				//Fadetoblack(7.0);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,14.0,false);
				playerai(epl_player0,true);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
			endcase;			
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************

