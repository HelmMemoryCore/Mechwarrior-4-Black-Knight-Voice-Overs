
//*********************************************************************************

fsm scrub02_modl : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer playermech;
		
        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
                                                //
        static boolean      obj1;               // This indicates whether Obj 1 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        //    

		static integer	    cinema_cut;
        static integer      wimpy;              // For the Recruit setting
        static boolean      reinforcements;     // This means the enemy is warned sooner and more enemies will come to help
        eternal boolean     modlmove;           // This is whether or not to cue up the MODL and get him moving
        eternal boolean     secondmodl;         // This is whether or not to cue up the second MODL and get him moving        
        static integer      MODLs;              // This is the MODL group
        eternal integer     MODL1;              // This is the first MODL
        eternal integer     MODL2;              // This is the second MODL
		static boolean      SKIES_CLEARED;      // This will be the global variable for Clear the Skies
        static boolean      CANYON_AMBUSHED;    // This will be the global variable for Canyon Ambush
        eternal boolean     cavalry;            // The reinforcements will show up
        static boolean      youwinner;          // You've won
        eternal boolean     runaway;            // Enemy will runaway after a little bit
        
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

        playermech  = getplayervehicle(epl_player0);	
    
		GroupAddObject(GroupObjectId(1),playermech);

        // Howler Group
        GroupAddObject(GroupObjectId(10),eve_Howler);
        GroupAddObject(GroupObjectId(10),ebu_Command_Center);
        GroupAddObject(GroupObjectId(10),ebu_Communications_Facility);

        // East Patrol - Forward Recon Patrol for the MODL
        GroupAddObject(GroupObjectId(21),eve_epatrol_1);
        GroupAddObject(GroupObjectId(21),eve_epatrol_2);
        GroupAddObject(GroupObjectId(21),eve_epatrol_3);
        //GroupAddObject(GroupObjectId(21),eve_epatrol_4);
        SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);
   
        // MODL Forward Guard
        GroupAddObject(GroupObjectId(30),eve_modlfg_1);
        GroupAddObject(GroupObjectId(30),eve_modlfg_2);
        //GroupAddObject(GroupObjectId(30),eve_modlfg_3);
        GroupAddObject(GroupObjectId(30),eve_modlfg_4);
        SetGroupAI(groupobjectid(30),GROUPAI_MOODSQUAD);

        // MODL Rear Guard
        GroupAddObject(GroupObjectId(31),eve_modlrg_1);
        GroupAddObject(GroupObjectId(31),eve_modlrg_2);
        GroupAddObject(GroupObjectId(31),eve_modlrg_3);
        GroupAddObject(GroupObjectId(31),eve_modlrg_4);
        SetGroupAI(groupobjectid(31),GROUPAI_MOODSQUAD);
        
        // The 2 MODLs
        GroupAddObject(GroupObjectId(32),eve_modl);
        GroupAddObject(GroupObjectId(32),eve_modl2);
        MODLs       = GroupObjectID(32);

        // The Reinforcements
        GroupAddObject(GroupObjectId(33),eve_reinforce_1);
        GroupAddObject(GroupObjectId(33),eve_reinforce_2);
        GroupAddObject(GroupObjectId(33),eve_reinforce_3);
        SetGroupAI(groupobjectid(33),GROUPAI_MOODSQUAD);
    	
        // First Helo Patrol
        GroupAddObject(GroupObjectId(41),eve_aircraft_10);
        GroupAddObject(GroupObjectId(41),eve_aircraft_11);
        GroupAddObject(GroupObjectId(41),eve_aircraft_12);
        SetGroupAI(groupobjectid(41),GROUPAI_MOODSQUAD);
        
        // Second Helo Patrol
        GroupAddObject(GroupObjectId(42),eve_aircraft_20);
        GroupAddObject(GroupObjectId(42),eve_aircraft_21);
        GroupAddObject(GroupObjectId(42),eve_aircraft_22);
        GroupAddObject(GroupObjectId(42),eve_aircraft_23);
        GroupAddObject(GroupObjectId(42),eve_aircraft_24);
        SetGroupAI(groupobjectid(42),GROUPAI_MOODSQUAD);

        // All Mechs besides Reinforcers
        GroupAddObject(GroupObjectId(43),eve_epatrol_1);
        GroupAddObject(GroupObjectId(43),eve_epatrol_2);
        GroupAddObject(GroupObjectId(43),eve_epatrol_3);
        //GroupAddObject(GroupObjectId(43),eve_epatrol_4);
        GroupAddObject(GroupObjectId(43),eve_modlfg_1);
        GroupAddObject(GroupObjectId(43),eve_modlfg_2);
        //GroupAddObject(GroupObjectId(43),eve_modlfg_3);
        GroupAddObject(GroupObjectId(43),eve_modlfg_4);
        GroupAddObject(GroupObjectId(43),eve_modlrg_1);
        GroupAddObject(GroupObjectId(43),eve_modlrg_2);
        GroupAddObject(GroupObjectId(43),eve_modlrg_3);
        GroupAddObject(GroupObjectId(43),eve_modlrg_4);
        
        // This is for the Black Jack audio order
        GroupAddObject(GroupObjectId(50),eve_epatrol_1);
        GroupAddObject(GroupObjectId(50),eve_epatrol_2);
        GroupAddObject(GroupObjectId(50),eve_epatrol_3);
        //GroupAddObject(GroupObjectId(50),eve_epatrol_4);
        GroupAddObject(GroupObjectId(50),eve_modlfg_1);
        GroupAddObject(GroupObjectId(50),eve_modlfg_2);
        //GroupAddObject(GroupObjectId(50),eve_modlfg_3);
        GroupAddObject(GroupObjectId(50),eve_modlfg_4);
		
        cinema_cut = 0; 						
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
	    //playsound(eso_start);
		setactivationdistance(1200);
		// settargetdesirability(playermech,-1);

        setradarnav(ena_Nav_Alpha,1);
        setradarnav(ena_Nav_Beta,2);
        //setradarnav(ena_Nav_Gamma,3);
        setradarnav(ena_Nav_Zeta,4);

        revealnavpoint(ena_Nav_Alpha);
        revealnavpoint(ena_Nav_Beta);
        //revealnavpoint(ena_Nav_Gamma);
        setnavpoint(ena_Nav_Alpha);

        revealobjective(eob_objective1);
        
        SKIES_CLEARED       = GetEventFlag("SKIES_CLEARED");
        CANYON_AMBUSHED     = GetEventFlag("CANYON_AMBUSHED");
        
        If (SKIES_CLEARED) then             // If Clear the Skies was completed then no helo patrols for them
            Shutdown(GroupObjectID(41));
            TeleporttoHell(GroupObjectID(41));
            Destroy(GroupObjectID(41));
            Shutdown(GroupObjectID(42));
            TeleporttoHell(GroupObjectID(42));
            Destroy(GroupObjectID(42));
        endif;
        
        if (CANYON_AMBUSHED) then           // If you did the ambush they get less reinforcements
            Destroy(eve_reinforce_1);
            Destroy(eve_reinforce_2);
        endif;

        StartMusic("Music\KickIt",3.5,3.0,55);

        SetTargetDesirability(eve_howler,-1);   // He's hiding, no one will shoot Howler

        Wimpy = GetDifficulty;

        // In easy mode take out the Myrmidon
        If (wimpy == EASY_DIFFICULTY) then
            Destroy(eve_epatrol_3);
            Destroy(eve_modlrg_4);
        endif;

        trans opening;
        //trans won;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("Music\ArcticAmbLoop",2.0,2.0,20);
	endif;
    
// Audio Queues

    if (not GetGlobalTrigger(10)) then          // East Scout Patrol approaching
        if (iswithin(playermech,GroupObjectID(21),1000)) then
            SetGlobalTrigger(10,true);
            PlayLancemateSound("VOX\OP2\MI2\Vanguard",1);
        endif;
    endif;
    
    if (not GetGlobalTrigger(11)) then          // Jet at the beginning's fly-by
        //if (timegreater(1,30)) then
        if (iswithin(playermech,eve_aircraft_01,900)) then
            SetGlobalTrigger(11,true);
            PlayLancemateSound("VOX\OP2\MI2\Buzz",1);
        endif;
    endif;
    
    if (not GetGlobalTrigger(12)) then          // MODL within range
        if (iswithin(playermech,GroupObjectID(50),950)) then
            SetGlobalTrigger(12,true);
            StartMusic("Music\EnmityRoadLoop",3.5,3.0,50);
            PlayVoiceOverOnce("VOX\OP2\MI2\Black", TALKER_ERI);
            PlayVoiceOverOnce("VOX\OP2\MI2\Black", TALKER_HOW);
            PlayVoiceOverOnce("VOX\OP2\MI2\Black2", TALKER_ERI);
            starttimer(3);  // This controls when the reinforcements come
        endif;
    endif;

    if (not GetGlobalTrigger(13)) then          // MODL rearguard within range
        if (iswithin(playermech,GroupObjectID(31),1200)) then
            SetGlobalTrigger(13,true);
            PlayLancemateSound("VOX\OP2\MI2\Rear",1);
        endif;
    endif;

    if (not GetGlobalTrigger(14)) then          // A MODL is destroyed
        if (GroupNumDead(MODLs)==1) then
            SetGlobalTrigger(14,true);
            PlayLancemateSound("VOX\OP2\MI2\Modl1",1);
        endif;
    endif;

    if (not GetGlobalTrigger(15)) then          // Mood Music for the MODL
        if (iswithin(playermech,MODLs,950)) then
            SetGlobalTrigger(15,true);
            StartMusic("Music\Push",0.0,3.0,60);
        endif;
    endif;

    if (not GetGlobalTrigger(16)) then          // Advice from Radcliffe
        if (isshot(MODLs)) then
            SetGlobalTrigger(16,true);
            PlayLancemateSound("VOX\OP2\MI2\Black",1);
        endif;
    endif;
            
// MODL Timing - This controls when the MODL shows up with its escorts

    if (not modlmove) then              // Timer 1 starts once the opening is done
        //if (GroupNumDead(GroupObjectID(21))==1) then
        //if (isshot(GroupObjectID(21))) then
        if (timegreater(1,35)) then
            modlmove=true;
            starttimer(2);      // Timer 2 is used for the second patrol to start moving
            killtimer(1);
        endif;
    endif;
    
    if (not secondmodl) then            // This is for the second MODL to start going
        if (timegreater(2,60)) then
            secondmodl=true;
            killtimer(2);
        endif;
    endif;
    
    if (not GetGlobalTrigger(20)) then  // This controls when the reinforcements arrive
        if (timegreater(3,120)) then    // They arrive after a set time after the Black Jack call (presuming enough folks are dead)
            if (GroupNumDead(GroupObjectID(43))>2) then
                SetGlobalTrigger(20,true);
                cavalry = true;
            endif;
        endif;
    endif;
    		
//Reveal the objectives

// Objective 1 - Destroy the MODL

    if (not obj1) then
        if (GroupAllDead(MODLs)) then
            successobjective(eob_objective1);
            obj1=true;
    		KillVoiceOvers;
            PlayVoiceOverOnce("VOX\OP2\MI2\Modl2", TALKER_ERI);
            PlayVoiceOverOnce("VOX\OP2\MI2\White", TALKER_ERI);
            PlayVoiceOverOnce("VOX\OP2\MI2\White", TALKER_HOW);
            PlayVoiceOverOnce("VOX\OP2\MI2\White2", TALKER_ERI);
            revealnavpoint(ena_Nav_Zeta);       // The player gets about a half minute before the game ends
                                                // Zeta is an illusory goal. This may need to change to fit VO better.
            setnavpoint(ena_Nav_Zeta);
            revealobjective(eob_objective2);
            starttimer(28);
        endif;
    endif;

    if (not YouWinner) then
        if (GroupAllDead(GroupObjectID(43))) then
            if (timegreater(28,17)) then
                YouWinner = true;
            endif;
        endif;
    endif;

    if (not RunAway) then
        if (timegreater(28,14)) then
            RunAway = true;
        endif;
    endif;

    if (timegreater(28,28)) then
        YouWinner = true;
    endif;
    
    if (YouWinner) then
        killtimer(28);
        playerai(epl_player0,true);
        endmission(true,30);

      // Salvage Added by Ferret 7/28/01         
        RemoveAllWeaponsFromMarket;
        RemoveAllMechsFromMarket;
        RemoveAllWeaponsFromSalvage;
        RemoveAllNonMissionMechsFromSalvage;
        
        AddWeaponSalvage("LargeLaser",4);
        AddWeaponSalvage("ERMediumLaser",6);
        AddWeaponSalvage("ERLargeLaser",4);
        AddWeaponSalvage("LRM15",3);
        AddWeaponSalvage("ClanLRM15",4);
        AddWeaponSalvage("LRM20",2);
        AddWeaponSalvage("ClanLBXAC20",1);
        AddWeaponSalvage("UltraAC20",1);
        AddWeaponSalvage("PPC",4);
        AddWeaponSalvage("LargeXPulseLaser",2);
        AddWeaponSalvage("MediumXPulseLaser",4);
        AddWeaponSalvage("SSRM2",2);
        AddWeaponSalvage("SSRM4",2);
        AddWeaponSalvage("SSRM6",2);
        AddWeaponSalvage("Gauss",2);
        AddWeaponSalvage("ClanGauss",1);
        AddWeaponSalvage("LightGauss",1);
        AddWeaponSalvage("ClanMachineGun",6);
        AddWeaponSalvage("ClanUltraAC2",1);
        
        AddMechInstanceSalvage("Thor");
        AddMechInstanceSalvage("Ryoken");
        AddMechInstanceSalvage("Thanatos");
        AddMechInstanceSalvage("Nova Cat");
        AddMechInstanceSalvage("Loki");
      //
        
        trans won;
    endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\EriDestroyed3",talker_MAN);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
        cinema_cut = 20;    // This is so it will go to state lost and just sit there
		Trans Lost;
	endif;
    
    if (isdead(eve_howler)) then    // If you kill Howler, then mission ends
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\Abort1",talker_MAN);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
		Trans Lost;
    endif;
    
    if (not GetGlobalTrigger(17)) then
        if (iswithin(MODLs,ena_MODLgoal,200)) then   // if the MODLs get to their destination, game over
    		KillVoiceOvers;
    		PlayVoiceOverOnce("VOX\Generic\Abort1",talker_MAN);
    		StartMusic("Music\Defeat",3.0,3.5,70);
            failobjective(eob_objective1);
            endmission(false,14);
            starttimer(14);
            SetGlobalTrigger(17,true);
        endif;
    endif;
		
    if (timegreater(14,4)) then        
        Trans Lost;
    endif;

// Nav Point Betty

    if (not alphareached) then
    	if (isnavrevealed(ena_nav_alpha)) then
    		if (iswithin(playermech,ena_nav_alpha,150)) then
    			playvoiceover("VO\generic\navalpha",talker_Bet);
                setnavpoint(ena_nav_beta);
    			alphareached = true;
    		endif;
    	endif;
    endif;
    
    if (not betareached) then
    	if (isnavrevealed(ena_nav_beta)) then
    		if (iswithin(playermech,ena_nav_beta,150)) then
    			playvoiceover("VO\generic\navbeta",talker_Bet);
    			betareached = true;
    		endif;
    	endif;
    endif;

    
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;

	code

    if (cinemaskip) then
        KillVoiceOvers;
        cinema_cut = 0;
        KillTimer(gti_timer_17);
        SetGlobalTrigger(0,true);
        playerai(epl_player0,false);
        SetCompositingEnabled(True);
    
        CinemaEnd;
        startup(GroupObjectID(21));
        startup(eve_aircraft_01);
        starttimer(1);
        trans sit;
    endif;

    switch (cinema_cut)
			
			case 0:
                PlayVoiceOverOnce("VOX\OP2\MI2\Intro", TALKER_HOW);
                PlayVoiceOverOnce("VOX\OP2\MI2\Intro2", TALKER_MAN);
                PlayVoiceOverOnce("VOX\OP2\MI2\Intro3", TALKER_MAN);
                
                SetCompositingEnabled(False);
    			CinemaStart;
                SetActiveCamera (eca_CameraShip);
                playerai(epl_player0,true);

                TargetFollowObject(eve_Howler);
                CameraFollowObject(eve_Howler);
                TargetOffset(0.0,5.0,0.0,0.0,false);
                TargetOffset(0.0,5.0,0.0,23.0,false);
                CameraOffset(0.0,15.0,100.0,0.0,true);
                CameraOffset(-80.0,10.0,0.0,23.0,true);
                
                FadeToBlack(0.0);
                FadeFromBlack(6.0);
                
                shutdown(GroupObjectID(21));    // The first patrol is shutdown during the movie
                shutdown(eve_aircraft_01);      // The airplane flyby is also postponed
                starttimer(17);
                
                cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
                if (timegreater(17,25)) then
                    TargetFollowObject(playermech);
                    PlayVoiceOverOnce("VOX\OP2\MI2\Readyop", TALKER_ERI);
                    PlayLancemateSound("VOX\OP2\MI2\Readyop", 3);
                    PlayVoiceOverOnce("VOX\OP2\MI2\Readyop2", TALKER_ERI);
                    PlayVoiceOverOnce("VOX\OP2\MI2\Readyop", TALKER_MAN);
                    CameraFollowObject(playermech);
                    TargetOffset(0.0,0.0,0.0,0.0,false);
                    TargetOffset(0.0,0.0,0.0,20.0,false);
                    CameraOffset(0.0,40.0,650.0,0.0,true);
                    CameraOffset(0.0,20.0,50.0,20.0,true);
                    
                    resettimer(17);
                    cinema_cut = cinema_cut +1;
                endif;
			endcase;

			case 2:
                if (timegreater(17,22)) then
                    KillVoiceOvers;
                    cinema_cut = 0;
                    KillTimer(gti_timer_17);
                    SetGlobalTrigger(0,true);
                    playerai(epl_player0,false);
                
                    CinemaEnd;
                    startup(GroupObjectID(21));
                    startup(eve_aircraft_01);
                    starttimer(1);
                    SetCompositingEnabled(True);
                    trans sit;
                endif;
			endcase;
			
		endswitch;
	
endstate;

state won;

		code
		
		if (cinemaskip) then
			cinema_cut = 20;
			playerai(epl_player0,false);
			cinemaend;
			KillVoiceOvers;
			endmission(true,1);
		endif;

		
		switch cinema_cut
		
			case 0:	
				SetCompositingEnabled(False);
                CinemaStart;
            	enableglobalinvulnerable;
        		StartMusic("Music\Victory2",3.0,3.5,45);
            	settargetdesirability(playermech,-1);
				Fadefromblack(0.0);
				playerai(epl_player0,true);
				ClearMoveOrder(playermech);
				setactivecamera (eca_modlcam);
				//Fadetoblack(7.0);
				Targetfollowobject(eve_modl);
				Camerafollowobject(eve_modl);
				Targetoffset(0.0,7.0,0.0,0.0,false);
				Cameraoffset(20.0,100.0,-90.0,0.0,false);
				Cameraoffset(-100.0,30.0,100.0,10.0,false);
				startTimer (gti_Timer_21);
				resettimer (gti_Timer_21);

                PlayVoiceOverOnce("VOX\OP2\MI2\White", TALKER_ERI);
                PlayVoiceOverOnce("VOX\OP2\MI2\White", TALKER_HOW);
                PlayVoiceOverOnce("VOX\OP2\MI2\White2", TALKER_ERI);
                PlayVoiceOverOnce("VOX\OP2\MI2\Victory", TALKER_MAN);
                PlayLancemateSound("VOX\OP2\MI2\Victory",2);
                PlayVoiceOverOnce("VOX\OP2\MI2\Victory", TALKER_ERI);
                if (PlayLancemateSound("VOX\OP2\MI2\Victory3",1)) then
                    PlayVoiceOverOnce("VOX\OP2\MI2\Victory2", TALKER_ERI);
                    PlayLancemateSound("VOX\OP2\MI2\Victory2",1);
                    //PlayLancemateSound("VOX\OP2\MI2\Victory4",1);   Sounded awkward and played LOUD.
                endif;
            
                endmission(true,35);    // was 49
                Cinema_Cut = Cinema_Cut+1;
			endcase;
			
			
			Case 1:
				If TimeGreater(GTI_Timer_21,12) then
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
                SetCameraFootShake(playermech,50);
				Cameraoffset(15.0,1.0,-15.0,0.0,false);
				Cameraoffset(-15.0,1.0,-15.0,3.0,false);	
				resettimer (gti_Timer_21);
				Cinema_Cut = Cinema_Cut+1;
				endif;
			endcase;

			Case 2:
				If TimeGreater(GTI_Timer_21,3) then
				Cameraoffset(-15.0,1.0,-15.0,0.0,false);
				Cameraoffset(-15.0,1.0,15.0,3.0,false);	
				resettimer (gti_Timer_21);
				Cinema_Cut = Cinema_Cut+1;
				endif;
			endcase;

			Case 3:
				If TimeGreater(GTI_Timer_21,3) then
				Cameraoffset(-15.0,1.0,15.0,0.0,false);
				Cameraoffset(15.0,1.0,15.0,3.0,false);	
				resettimer (gti_Timer_21);
				Cinema_Cut = Cinema_Cut+1;
				endif;
			endcase;
            			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
				SetCompositingEnabled(False);
                CinemaStart;
				Fadefromblack(3.0);
				playerai(epl_player0,true);
				ClearMoveOrder(playermech);
				setactivecamera (eca_modlcam);
				//Fadetoblack(7.0);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,7.0,0.0,0.0,false);
				Cameraoffset(20.0,100.0,-90.0,0.0,false);
				Cameraoffset(-100.0,30.0,100.0,12.0,false);
				startTimer (gti_Timer_21);
				resettimer (gti_Timer_21);
                
                cinema_cut=cinema_cut+1;
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

state hang;
        code
endstate;

endfsm.
//******************************************************************

