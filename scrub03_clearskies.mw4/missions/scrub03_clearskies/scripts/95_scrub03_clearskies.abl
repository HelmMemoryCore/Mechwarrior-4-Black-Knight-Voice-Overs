
//*********************************************************************************

fsm scrub03_clearskies : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
        
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer playermech;

        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
        static integer      checkrange;         // How far from the Nav Point you have to be                                            
                                                //
        eternal boolean     obj1;               // This indicates whether Obj 1 was completed
        eternal boolean     obj2;               // This indicates whether Obj 2 was completed
        eternal boolean     obj3;               // This indicates whether Obj 3 was completed
        eternal boolean     obj4;               // This indicates whether Obj 4 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        static boolean      obj2revealed;       // This indicates that Obj 2 has been revealed
        static boolean      obj3revealed;       // This indicates that Obj 3 has been revealed
        static boolean      obj4revealed;       // This indicates that Obj 4 has been revealed
        //    

        static integer      wimpy;              // Whether a player is doing the easy mission
        eternal boolean     rebelfight;         // The Rebel and Dressari fight flag.
        static boolean      baseguards;         // The base guards will start to power up flag.
        static boolean      rebelsdead;         // This indicates the rebels have died.
		
		static integer	    cinema_cut;
        eternal boolean     basestart;          // Whether or not the Base mechs are on alert
        eternal boolean     base1;              // Whether to power up the first base mech
        eternal boolean     base2;              // Whether to power up the second base mech
        static integer      timervar;           // This is the timer for the end movie - it is different depending on which ending you get
        eternal boolean     alarm;              // Whether anybody is now on alert       
        eternal boolean     firstspot;          // This is when the PCs are first spotted
        eternal boolean     fightergo;          // If you get too close to the fighters they take off
		
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

		    playermech = getplayervehicle(epl_player0);	
 	
			GroupAddObject(GroupObjectId(1),playermech);
            // SetGroupAI(GroupObjectId(1),GROUPAI_FOCUSFIRESQUAD);

            // Rebels at Nav Gamma
            GroupAddObject(GroupObjectId(2),eve_rebel_1);
            GroupAddObject(GroupObjectId(2),eve_rebel_2);
            SetGroupAI(GroupObjectId(2),GROUPAI_MOODSQUAD);

            // This is the North Patrol
            GroupAddObject(GroupObjectId(10),eve_NPatrol_1);
            GroupAddObject(GroupObjectId(10),eve_NPatrol_2);
            GroupAddObject(GroupObjectId(10),eve_NPatrol_3);
            SetGroupAI(GroupObjectId(10),GROUPAI_MOODSQUAD);

            // This is the South Patrol (its heavier)
            GroupAddObject(GroupObjectId(11),eve_SPatrol_1);
            GroupAddObject(GroupObjectId(11),eve_SPatrol_2);
            GroupAddObject(GroupObjectId(11),eve_SPatrol_3);
            SetGroupAI(GroupObjectId(11),GROUPAI_MOODSQUAD);

            // Two Air Base guard Mechs which start powered down 
            GroupAddObject(GroupObjectId(12),eve_baseguard_1);
            GroupAddObject(GroupObjectId(12),eve_baseguard_2);
            SetGroupAI(GroupObjectId(12),GROUPAI_MOODSQUAD);

            // Two Elite Dressari Mechs making short work of the Rebels
            GroupAddObject(GroupObjectId(13),eve_dressari_1);
            GroupAddObject(GroupObjectId(13),eve_dressari_2);
            SetGroupAI(GroupObjectId(13),GROUPAI_MOODSQUAD);
            
            // This is to make sure that the game can afford the resources for the Rebel Fight
            GroupAddObject(GroupObjectId(14),eve_NPatrol_1);
            GroupAddObject(GroupObjectId(14),eve_NPatrol_2);
            GroupAddObject(GroupObjectId(14),eve_NPatrol_3);
            GroupAddObject(GroupObjectId(14),eve_SPatrol_1);
            GroupAddObject(GroupObjectId(14),eve_SPatrol_2);
            GroupAddObject(GroupObjectId(14),eve_SPatrol_3);
            GroupAddObject(GroupObjectId(14),eve_baseguard_1);
            GroupAddObject(GroupObjectId(14),eve_baseguard_2);

            // All the Base Turrets
            GroupAddObject(GroupObjectId(21),etu_bigturret_1);
            GroupAddObject(GroupObjectId(21),etu_bigturret_2);
            GroupAddObject(GroupObjectId(21),etu_bigturret_3);
            GroupAddObject(GroupObjectId(21),etu_bigturret_4);
            GroupAddObject(GroupObjectId(21),etu_bigturret_5);
            GroupAddObject(GroupObjectId(21),etu_smallturret_1);
            GroupAddObject(GroupObjectId(21),etu_smallturret_2);
            GroupAddObject(GroupObjectId(21),eve_baseguard_1);
            GroupAddObject(GroupObjectId(21),eve_baseguard_2);
            SetGroupAI(GroupObjectId(21),GROUPAI_MOODSQUAD);

            // The Helo Patrol at the beginning
            GroupAddObject(GroupObjectId(31),eve_NHelo_1);
            GroupAddObject(GroupObjectId(31),eve_NHelo_2);
            GroupAddObject(GroupObjectId(31),eve_NHelo_3);
            GroupAddObject(GroupObjectId(31),eve_NHelo_4);
            GroupAddObject(GroupObjectId(31),eve_NHelo_5);
            GroupAddObject(GroupObjectId(31),eve_NHelo_6);
            SetGroupAI(GroupObjectId(31),GROUPAI_FOCUSFIRESQUAD);

            // The Helo Patrol near the base
            GroupAddObject(GroupObjectId(32),eve_SHelo_1);
            GroupAddObject(GroupObjectId(32),eve_SHelo_2);
            GroupAddObject(GroupObjectId(32),eve_SHelo_3);
            GroupAddObject(GroupObjectId(32),eve_SHelo_4);
            GroupAddObject(GroupObjectId(32),eve_SHelo_5);
            SetGroupAI(GroupObjectId(32),GROUPAI_FOCUSFIRESQUAD);
            
            // The base copters
            GroupAddObject(GroupObjectId(33),eve_basecopter_1);
            GroupAddObject(GroupObjectId(33),eve_basecopter_2);
            GroupAddObject(GroupObjectId(33),eve_basecopter_3);
            GroupAddObject(GroupObjectId(33),eve_basecopter_4);
            GroupAddObject(GroupObjectId(33),eve_basecopter_5);
            GroupAddObject(GroupObjectId(33),eve_basecopter_6);
            GroupAddObject(GroupObjectId(33),eve_basecopter_7);
            GroupAddObject(GroupObjectId(33),eve_basecopter_8);

            SetSensorVisibility(ebu_air_control_tower,true);
            SetSensorVisibility(ebu_aircraft_hangar,true);

    		SetTarget(ede_people_desert,eve_basecopter_6);
            SetTarget(ede_people_desert_0001,eve_basecopter_6);
            SetTarget(ede_people_desert_0003,eve_basecopter_6);
            SetTarget(ede_people_desert_0005,eve_basecopter_6);
            SetTarget(ede_people_desert_0007,eve_basecopter_6);
            SetTarget(ede_people_desert_0009,ede_people_desert);
            SetTarget(ede_people_desert_0011,ede_people_desert);
            SetTarget(ede_people_desert_0013,ede_people_desert);
            SetTarget(ede_people_desert_0015,ebu_mech_hanger);
            SetTarget(ede_people_desert_0017,ebu_mech_hanger);
            SetTarget(ede_people_desert_0019,ebu_air_control_tower);
            SetTarget(ede_people_desert_0021,ebu_aircraft_hangar);
            SetTarget(ede_people_desert_0023,ebu_communications_dish);
            SetTarget(ede_people_desert_0025,ebu_shilone_hangar);
            SetTarget(ede_people_desert_0025,etu_bigturret_4);
 		
    		cinema_cut = 0; 						
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
        setradarnav(ena_Nav_Alpha,1);
        setradarnav(ena_Nav_Beta,2);
        setradarnav(ena_Nav_Gamma,1);
        
        setactivationdistance(1200);

        revealnavpoint(ena_Nav_Alpha);
        setnavpoint(ena_Nav_Alpha);
        
//        settargetdesirability(playermech,-1);
        //playsound(eso_start);
        //setactivationdistance(6800);
        //trans opening;
//        playerai(ep1_player0,false);
//        cinemaend;

        setsensorvisibility(eve_stiletto,false);
        setsensorvisibility(eve_ambulance,false);
        setsensorvisibility(eve_basecopter_6,false);
        
        Wimpy = GetDifficulty;

        // In easy mode take out the two powered down base 'Mechs
        If (wimpy == EASY_DIFFICULTY) then
            Shutdown(eve_baseguard_1);
            Shutdown(eve_baseguard_2);
            TeleporttoHell(eve_baseguard_1);
            TeleporttoHell(eve_baseguard_2);
            Destroy(eve_baseguard_1);
            Destroy(eve_baseguard_2);
        endif;
            
        StartMusic("Music\SlowBurn",3.5,3.0,50);

        playerai(epl_player0,true);
        trans opening;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

	if IsMusicPlaying == false then
		StartMusic("Music\ArcticAmbLoop",2.0,2.0,20);
	endif;

// Sound Triggers

    if (not GetGlobalTrigger(2)) then   // Just a little more info from the Intro
        if (timegreater(1,4)) then
            PlayVoiceOverOnce("VOX\OP2\MI3\Caution",talker_MAN);
            SetGlobalTrigger(2,true);
        endif;
    endif;

    if (not GetGlobalTrigger(3)) then   // First patrol down
        if (groupalldead(groupobjectid(10))) then
            PlayVoiceOverOnce("VOX\OP2\MI3\scratchone",talker_ERI);
            SetGlobalTrigger(3,true);
        else            
            if (groupalldead(groupobjectid(11))) then
                PlayVoiceOverOnce("VOX\OP2\MI3\scratchone",talker_ERI);
                SetGlobalTrigger(3,true);
            endif;
        endif;
    endif;
    
    if (not GetGlobalTrigger(4)) then   // Once you get close to the Rebel fight
        if (obj2) then
            if (iswithin(playermech,ena_nav_gamma,800)) then                
                if (random(2)==1) then
                    if (PlayLancemateSound("VOX\OP2\MI3\Spot",1)) then
                        PlayVoiceOverOnce("VOX\OP2\MI3\Spot",talker_ERI);
                        SetGlobalTrigger(5,true);   // This is to check if a line was spoken
                    endif;
                else
                    if (PlayLancemateSound("VOX\OP2\MI3\Fight",1)) then
                        PlayVoiceOverOnce("VOX\OP2\MI3\Fight",talker_ERI);
                        SetGlobalTrigger(5,true);
                    endif;
                endif;
                if (not GetGlobalTrigger(5)) then   // If no one spoke, try playing OLS & ISA again
                    if (PlayLancemateSound("VOX\OP2\MI3\Fight",1)) then
                        PlayVoiceOverOnce("VOX\OP2\MI3\Fight",talker_ERI);
                    endif;
                endif;
                SetGlobalTrigger(4,true);
            endif;
        endif;
    endif;

    if (not GetGlobalTrigger(6)) then   // If one of the Rebels dies
        if (obj2) then
            if (GroupNumDead(GroupObjectID(2))==1) then
                PlayVoiceOverOnce("VOX\OP2\MI3\Rebels2",talker_REB);
                SetGlobalTrigger(6,true);
            endif;
        endif;
    endif;

// Triggers
	if (not basestart) then     // These triggers control when the Base guards power up and fight
        if (IsWithin(playermech,ena_Nav_Alpha,800)) then
            basestart=true;
            starttimer(12);
        endif;
    endif;
    
    if (not base1) then
        if (timegreater(12,15)) then    // was 45
            base1=true;
        endif;
    endif;
    
    if (not base2) then
        if (timegreater(12,35)) then    // was 65
            base2=true;
        endif;
    endif;
    
    if (not GetGlobalTrigger(7)) then       // Once the PCs are spotted it takes the enemy a minute to mobilize
        if (firstspot) then
            starttimer(5);
            SetGlobalTrigger(7,true);
        endif;
    endif;
    
    if (not alarm) then     // Now the enemy starts converging on the PCs
        if (timegreater(5,60)) then
            alarm = true;
        endif;
    endif;    

    if (not firstspot) then // If you get too close to the jet fighters they mobilize
        if (not fightergo) then
            if (iswithin(playermech,eve_wing_1,1500)) then
                fightergo = true;
            endif;
        endif;
    endif;
    
// Reveal the objectives

// Objective 1 - Destroy the Mech Patrols outside the air base.

    if (not obj1) then
        if (groupalldead(groupobjectid(10))) then
            if (groupalldead(groupobjectid(11))) then
                successobjective(eob_objective1);
                PlayVoiceOverOnce("VOX\OP2\MI3\Scratchtwo",talker_ERI);
                PlayVoiceOverOnce("VOX\OP2\MI3\Scratchtwo",talker_MAN);
                if (iswithin(playermech,ena_nav_alpha,700)) then
                    PlayVoiceOverOnce("VOX\OP2\MI3\Scratchtwo2",talker_ERI);
                endif;
                obj1=true;
            endif;
        endif;
    endif;

// Objective 2 - Destroy the Hangar and Control Tower.

    if (not baseguards) then
        if (IsWithin(playermech,ena_Nav_Alpha,1200)) then
            // successobjective(eob_objective1a);
            // revealobjective(eob_objective2);
            baseguards = true;
        endif;
    endif;

    if (not obj2) then
        if (isdead(ebu_air_control_tower)) then
            if (isdead(ebu_aircraft_hangar)) then
                successobjective(eob_objective2);
                starttimer(3);  // Give a delay before the next objectives pop up
                obj2=true;
                revealnavpoint(ena_nav_gamma);
                // setnavpoint(ena_nav_gamma);
                revealobjective(eob_objective3);
                revealnavpoint(ena_nav_beta);
                setnavpoint(ena_nav_beta);
                revealobjective(eob_objective4);
                StartMusic("Music\Wednesday",3.5,3.0,50);
                PlayVoiceOverOnce("VOX\OP2\MI3\Rebels",talker_MAN);
                PlayVoiceOverOnce("VOX\OP2\MI3\Rebels",talker_REB);
                PlayVoiceOverOnce("VOX\OP2\MI3\Rebels2",talker_MAN);
                PlayLancemateSound("VOX\OP2\MI3\Rebels",1);
                PlayVoiceOverOnce("VOX\OP2\MI3\Court",talker_MAN);
            endif;
        endif;
    endif;
    
// Objective 3 - Help out the rebels.

    if (not GetGlobalTrigger(10)) then
        if (obj2) then
            if (iswithin(playermech,ena_nav_gamma,1400)) then
                rebelfight = true;
                SetGlobalTrigger(10,true);
            endif;
        endif;
    endif;
    
/*   if (not GetGlobalTrigger(10)) then  // After 210 seconds of game time the rebels will need help
        if (timegreater(1,210)) then
            if (groupnumdead(GroupObjectID(14)) > 2) then
                if (timegreater(9,45)) then // This makes sure the player has been away from Nav Gamma for
                                            // at least 45s. This means it is possible for the rebels to
                                            // never show up. That's fine, though. The player is going way
                                            // off the beaten path.
                    revealnavpoint(ena_Nav_Gamma);
                    revealobjective(eob_objective3);
                    rebelfight = true;
                    SetGlobalTrigger(10,true);
                endif;
            endif;
        endif;
    endif; 
    
    if (not GetGlobalTrigger(10)) then  // If the player manages to kill 6 Mechs quickly, play the Rebel message
        if (not timegreater(1,210)) then
            if (groupnumdead(GroupObjectID(14)) > 5) then
                if (timegreater(9,45)) then
                    revealnavpoint(ena_Nav_Gamma);
                    revealobjective(eob_objective3);
                    rebelfight = true;
                    SetGlobalTrigger(10,true);
                endif;
            endif;
        endif;
    endif; */

    if (not GetGlobalTrigger(11)) then
        if (groupalldead(groupobjectid(2))) then
            failobjective(eob_objective3);
            hideobjective(eob_objective4);
            rebelsdead = true;
            //SQUIRE_FRIEND = false;
            obj3=false;
            SetGlobalTrigger(11,true);
            timervar = 0;   // Won rebels dead
            starttimer(19);
        endif;
    endif;
    
    if (not GetGlobalTrigger(11)) then  // The 11 trigger is for whether Nav Gamma has been dealt with
        if (groupalldead(groupobjectid(13))) then
            if (rebelsdead==false) then
                successobjective(eob_objective3);
                hideobjective(eob_objective4);
                SetEventFlag("SQUIRE_FRIEND",TRUE);
                obj3=true;
                SetGlobalTrigger(11,true);
                timervar = 1;   // Won w/rebels
                starttimer(19);
            endif;
        endif;
    endif;

// Objective 4 - Retreat to Beta

    if (obj2) then
        if (not obj4) then
            if (iswithin(playermech,ena_nav_beta,150)) then
                // SQUIRE_FRIEND = false;
                obj4 = true;
                successobjective(eob_objective4);
                failobjective(eob_objective3);
                timervar = 0;   // Won ignore rebels
                starttimer(19);
            endif;
        endif;
    endif;
                    
// Fail the mission when the player dies
	
    if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\EriDestroyed2",talker_MAN);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
        cinema_cut = 20;    // This is so it will go to state lost and just sit there
		Trans Lost;
	endif;
    
// Delay the mission end

    if (obj1) then      // Placed here to make sure the patrols have been killed
        if (timegreater(19,5)) then
            
          // Salvage Added by Ferret 7/28/01         
            AddWeaponSalvage("SSRM2",4);
            AddWeaponSalvage("LRM15",1);            
            AddWeaponSalvage("LargePulseLaser",2);
            
            AddMechInstanceSalvage("Catapult");
			AddMechInstanceSalvage("Uziel");
            
			If (wimpy == EASY_DIFFICULTY) then
				AddMechInstanceSalvage("Mad Cat");
			endif;
            
            AddWeaponToMarket("ClanUltraAC2",2);
            AddWeaponToMarket("ClanUltraAC5",2);
            AddWeaponToMarket("LightGauss",1);
            AddWeaponToMarket("ClusterBomb",2);
            AddWeaponToMarket("UltraAC10",2);
            AddWeaponToMarket("ClanLRM10",2);

            AddMechToMarket("Vulture");
			AddMechToMarket("Ryoken");
          //

            SetEventFlag("SKIES_CLEARED",TRUE);
          
            if (timervar==0) then
                trans won;
            else
                trans wonrebel;
            endif;
        endif;
    endif;
                                        
endstate;        

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;

	code

	if (cinemaskip) then
        cinema_cut = 0;
        resettimer (gti_Timer_21);
        killtimer(gti_timer_21);
        playerai(epl_player0,false);
        killvoiceovers;
        cinemaend;
        SetCompositingEnabled(True);
        starttimer(1);  // This is the mission timer from the beginning
        shutdown(eve_plane_1);
        shutdown(eve_introgimmick);
        teleporttohell(eve_plane_1);
        teleporttohell(eve_introgimmick);
        destroy(eve_plane_1);
        destroy(eve_introgimmick);
        revealobjective(eob_Objective1);            
        revealobjective(eob_Objective2);            
        trans sit;	
	endif;

 	switch (cinema_cut)
			
			case 0:

                SetCompositingEnabled(False);
                cinemastart;
                playerai(epl_player0,true);
                setactivecamera (eca_cameraship1);
                fadetoblack(0.0);
                fadefromblack(3.0);
                
                PlayVoiceOverOnce("VOX\OP2\MI3\Intro",talker_MAN);
                PlayVoiceOverOnce("VOX\OP2\MI3\Intro",talker_ERI);
                PlayVoiceOverOnce("VOX\OP2\MI3\Intro2",talker_MAN);

                targetfollowobject(eve_plane_1);
                camerafollowobject(ede_introcam);
                cameraoffset(0.0,30.0,0.0,0.0,false);
    
                startTimer (gti_Timer_21);
                shutdown(eve_plane_1);
                
                cinema_cut = cinema_cut + 1;
    		endcase;
			
			case 1:
                if (timegreater(21,4)) then
                    startup(eve_plane_1);
                    cinema_cut = cinema_cut + 1;
                endif;
            endcase;

			case 2:
                if (timegreater(21,25)) then
                    PlayVoiceOverOnce("VOX\OP2\MI3\Scanners",talker_ERI);
                    PlayLancemateSound("VOX\OP2\MI3\Scanners",1);
                    targetfollowobject(playermech);
                    camerafollowobject(playermech);
                    targetoffset(0.0,4.0,0.0,0.0,false);
                    cameraoffset(-140.0,49.0,4.0,0.0,false);
                    cameraoffset(-90.0,35.0,12.0,12.0,false);
                    cinema_cut = cinema_cut + 1;
                endif;
			endcase;
			
			case 3:
                if (timegreater(gti_Timer_21,43)) then
                    cinema_cut = 0;
                    resettimer (gti_Timer_21);
                    killtimer(gti_timer_21);
                    playerai(epl_player0,false);
                    killvoiceovers;
                    cinemaend;
                    SetCompositingEnabled(True);
                    starttimer(1);  // This is the mission timer from the beginning
                    shutdown(eve_plane_1);
                    shutdown(eve_introgimmick);
                    teleporttohell(eve_plane_1);
                    teleporttohell(eve_introgimmick);
                    destroy(eve_plane_1);
                    destroy(eve_introgimmick);
                    revealobjective(eob_Objective1);            
                    revealobjective(eob_Objective2);            
                    trans sit;	
                endif;
			endcase;
			
		endswitch;
	
endstate;

state wonrebel;
	code
		
		if (cinemaskip) then
			cinema_cut = 20;
			playerai(epl_player0,false);
			cinemaend;
			KillVoiceOvers;
			Endmission(true,1);
		endif;
		
		switch cinema_cut
            
            case 0:
				SetCompositingEnabled(False);
                CinemaStart;
				FadeToBlack(0.0);
				FadeFromBlack(3.0);
            	StartMusic("Music\Victory2",3.0,3.5,70);

                PlayVoiceOverOnce("VOX\OP2\MI3\Identify",talker_SQU);
                PlayLancemateSound("VOX\OP2\MI3\Identify",1);
                PlayVoiceOverOnce("VOX\OP2\MI3\Identify",talker_ERI);
                PlayVoiceOverOnce("VOX\OP2\MI3\Identify2",talker_SQU);
                PlayVoiceOverOnce("VOX\OP2\MI3\Identify2",talker_ERI);
                PlayVoiceOverOnce("VOX\OP2\MI3\Identify3",talker_SQU);
                
                endmission(true,25 + timervar);
				
				playerai(epl_player0,true);
				ClearMoveOrder(playermech);
                setactivecamera (eca_cameraship);
				//Fadetoblack(7.0);
                Targetfollowobject(playermech);
                Camerafollowobject(playermech);
                Targetoffset(0.0,4.0,0.0,0.0,false);
                Cameraoffset(10.0,4.0,-150.0,0.0,false);
                Cameraoffset(-120.0,60.0,50.0,10.0,false);
				startTimer (gti_Timer_21);
				resettimer (gti_Timer_21);
				cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
			
				if (timegreater(gti_Timer_21,10)) then
    				if (isdead(eve_rebel_1)) then
                        Targetfollowobject(eve_rebel_2);
    				    Camerafollowobject(eve_rebel_2);
                    else
                        Targetfollowobject(eve_rebel_1);
    				    Camerafollowobject(eve_rebel_1);
                    endif;
    				Targetoffset(0.0,4.0,0.0,0.0,false);
    				Cameraoffset(0.0,80.0,-100.0,0.0,false);
    				Cameraoffset(50.0,80.0,100.0,35.0,false);
    				cinema_cut = cinema_cut + 1;
				endif;

			endcase;
			
		endswitch;
	
endstate;

state won;
	code
		
		if (cinemaskip) then
			cinema_cut = 20;
			playerai(epl_player0,false);
			cinemaend;
			KillVoiceOvers;
			Endmission(true,1);
		endif;
		
		switch cinema_cut
            
            case 0:
				SetCompositingEnabled(False);
                CinemaStart;
				FadeToBlack(0.0);
				FadeFromBlack(5.0);

                PlayVoiceOverOnce("VOX\OP2\MI3\Exit",talker_ERI);
            	StartMusic("Music\Victory",3.0,3.5,70);
                if (not isdead(eve_dressari_1)) then
                    TeleporttoHell(eve_dressari_1);
                endif;
                if (not isdead(eve_dressari_2)) then
                    TeleporttoHell(eve_dressari_2);
                endif;
                //SetAlignment(GroupObjectID(13),friendly);
                //OrderFormationMove(GroupObjectID(13),epa_nhelopath,70,MOVE_CIRCLE,FORMATION_2x6line,FORMTYPE_NORMAL,true);
                
                endmission(true,17 + timervar);
				
				playerai(epl_player0,true);
				ClearMoveOrder(playermech);
                setactivecamera (eca_cameraship);
				//Fadetoblack(7.0);
                Targetfollowobject(playermech);
                Camerafollowobject(playermech);
                Targetoffset(0.0,4.0,0.0,0.0,false);
                Cameraoffset(10.0,4.0,-150.0,0.0,false);
                Cameraoffset(-120.0,60.0,50.0,13.0,false);
				startTimer (gti_Timer_21);
				resettimer (gti_Timer_21);
				cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
				if (timegreater(gti_Timer_21,10)) then
				endif;
			endcase;
			
		endswitch;
	
endstate;
			
state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;


endfsm.
//******************************************************************

