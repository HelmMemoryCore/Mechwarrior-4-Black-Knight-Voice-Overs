
//------------------------------------------------------------------
// NOTE: The fsm name below MUST be changed in order for the
// script to be considered a unique entity!

fsm fighterwing : integer;


//------------------------------------------------------------------
// Fighter Wing:
//   This script controls the jets near the base that will go to
//   PCs location during the mission.
//------------------------------------------------------------------



//------------------------------------------------------------------
//     Constants
//------------------------------------------------------------------

	const
		#include_ <content\ABLScripts\mwconst.abi>

//------------------------------------------------------------------
//     Types
//------------------------------------------------------------------

	type
		#include_ <content\ABLScripts\mwtype.abi>
	

//------------------------------------------------------------------
//     Variables
//------------------------------------------------------------------

	var
		static integer			attackRange;		// At what range do I start shooting?
		static integer			withdrawRange;		// At what range do I withdraw from combat completely?

		static integer			piloting;			// Piloting skill
		static integer			gunnery;			// Gunnery skill/chance to hit
		static real				minDelay;			// Minimum amount of time I will wait between shots once a weapon is reloaded
		static real				maxDelay;			// Maximum amount of time I will wait between shots once a weapon is reloaded
		static integer 			eliteLevel;			// Elite level.  This helps determine tactics, defensive manuevers, opportunity fire
													// and other things.  It indicates a general level of combat experience.
		static integer			attackThrottle;		// My attack throttle
		static integer			findTypes;			// What kind of enemies to look for

 		static integer			isShotRadius;		// How far away from me will I detect an enemy shot?

		static integer 			attackSound;		// The attack sound I play when I first attack
		static integer 			deathSound;			// The sound I play when I die
		
		static integer			mood;				// My default mood.

		static integer			takeOffDistance;	// My take-off distance if I'm an airplane (ignored if not an airplane)
        static boolean          timeroff;           // The shutdown timer is now counting.
        static locpoint         mystart1;           // This is the starting location used for teleport
        static locpoint         mystart2;           // This is the starting location used for teleport
        static locpoint         mystart3;           // This is the starting location used for teleport
        static locpoint         mystart4;           // This is the starting location used for teleport
        static boolean          goboom;             // The first Rebel Mech will blow up after a time
        static integer          groupis;            // Which integer group the unit is in
        static integer          path;               // This is the path of the units

//------------------------------------------------------------------
//     Init: my initialization function
//------------------------------------------------------------------

function Init;
	code
		// script-specific variables
		attackRange		= 900;
		withdrawRange	= attackRange * 3 / 2;
		takeOffDistance	= 450;

		// driver settings
		piloting		= 90;
		gunnery			= 220;
		minDelay		= 0.1;
		maxDelay		= 0.4;
		eliteLevel		= 90;
		attackThrottle	= 90;
		isShotRadius	= 120;
		attackSound		= -1; // no sound
		deathSound		= -1; // no sound
		mood			= NEUTRAL_START;
		findTypes		= FT_DEFAULT;

        groupis = GetMemoryInteger(me,9);

		if (GetMemoryInteger(me,9)==0) then         // Integer 0 = Fly By Plane
		    piloting		= 80;
		    gunnery			= 80;
	    	minDelay		= 0.1;
    		maxDelay		= 0.4;
            eliteLevel		= 90;
            path = -1;
		endif;

endfunction;

//------------------------------------------------------------------
//	StartState: my initial state
//------------------------------------------------------------------

state StartState;

	code
		SetFiringDelay			(ME,minDelay,maxDelay);
		SetIgnoreFriendlyFire	(ME,true);
		SetIsShotRadius			(ME,isShotRadius);
		SetEntropyMood			(ME,mood);
		SetCurMood				(ME,mood);
		SetSkillLevel			(ME,piloting,gunnery,eliteLevel);
		SetAttackThrottle		(ME,attackThrottle);

        shutdown(me);
        trans lobby;
        
endstate;

state lobby;
    code
				
		if (firstspot) then         // Int0 = Jet Patrol  
            startup(me);
            trans intheair;
		endif;
        
        if (fightergo) then
            startup(me);
            trans intheair;
        endif;

endstate;

state intheair;
    code
    
        if (OrderTakeOff(TakeOffDistance)) then
            trans gettoplayer;
        endif;

endstate;

//------------------------------------------------------------------
//	Gettoplayer: Run Over to the Player
//------------------------------------------------------------------

state Gettoplayer;
	code

        if (FindEnemy(attackRange,findTypes)) then
			trans AttackState;
		endif;

		ordermovetoobject(playermech,600);
        
endstate;

//------------------------------------------------------------------
//	AttackState: look for something to shoot
//------------------------------------------------------------------

state AttackState;
	code

        if (LeaveAttackState(withdrawRange)) then
			OrderStopAttacking;
			SetTarget(ME,NO_UNIT);
			trans gettoplayer;
		endif;

		OrderAttack(true);

endstate;

//------------------------------------------------------------------
//	DeadState: OK, I kicked the bucket.
//------------------------------------------------------------------

state DeadState;
	code
		if (deathSound <> -1) then	
			PlaySoundOnce(deathSound);
		endif;		

		orderDie;

endstate;


endfsm.

