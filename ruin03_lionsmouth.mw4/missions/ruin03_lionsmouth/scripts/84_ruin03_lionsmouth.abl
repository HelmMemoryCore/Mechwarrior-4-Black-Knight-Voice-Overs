
//*********************************************************************************

fsm Lionsmouth : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer     playermech;
		
        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
        static integer      checkrange;         // How far from the Nav Point you have to be                                            
                                                //
        eternal boolean     obj1;               // This indicates whether Obj 1 was completed
        eternal boolean     obj2;               // This indicates whether Obj 2 was completed
        eternal boolean     obj3;               // This indicates whether Obj 3 was completed
        eternal boolean     obj4;               // This indicates whether Obj 4 was completed
        eternal boolean     obj5;               // This indicates whether Obj 5 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        static boolean      obj2revealed;       // This indicates that Obj 2 has been revealed
        static boolean      obj3revealed;       // This indicates that Obj 3 has been revealed
        static boolean      obj4revealed;       // This indicates that Obj 4 has been revealed
        //    
		
		static integer	    cinema_cut;
        static integer      wimpy;              // For the easy setting
        eternal boolean     EnterTheRebel;      // Rebel Mechs come in to kick butt
        eternal boolean     EnterTheEnemy;      // Enemy Mechs come in to reinforce
        eternal boolean     SecondCin;          // This is for the end movies
        static integer      MissionCounter;     // How many objectives have been completed
        eternal integer     Fleeing;            // This determines how many Mechs have fled
        eternal boolean     REBEL_FRIEND;       // Global Variable for whether the rebels are your pals
		
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

            playermech = getplayervehicle(epl_player0);	
 	
			GroupAddObject(GroupObjectId(1),playermech);

            // Rebel Group from the North
            GroupAddObject(GroupObjectId(10),eve_rebel_01);
            GroupAddObject(GroupObjectId(10),eve_rebel_02);
            GroupAddObject(GroupObjectId(10),eve_rebel_03);
            // GroupAddObject(GroupObjectId(10),eve_rebel_04);
            SetGroupAI(groupobjectid(10),GROUPAI_MOODSQUAD);

			// First Group of Mechs - before Rebels and others teleport in
            GroupAddObject(GroupObjectId(20),eve_assault_01);
            GroupAddObject(GroupObjectId(20),eve_assault_02);
            GroupAddObject(GroupObjectId(20),eve_defense_01);
            GroupAddObject(GroupObjectId(20),eve_defense_02);
            //GroupAddObject(GroupObjectId(20),eve_defense_03);
            GroupAddObject(GroupObjectId(20),eve_defense_04);
            GroupAddObject(GroupObjectId(20),eve_defense_05);
            GroupAddObject(GroupObjectId(20),eve_defense_06);

            // Enemy Mechs at the beginning coming in on an assault            
            GroupAddObject(GroupObjectId(21),eve_assault_01);
            GroupAddObject(GroupObjectId(21),eve_assault_02);
            GroupAddObject(GroupObjectId(21),eve_assault_03);
            GroupAddObject(GroupObjectId(21),eve_assault_04);
            GroupAddObject(GroupObjectId(21),eve_assault_05);
            GroupAddObject(GroupObjectId(21),eve_assault_06);
            SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);

            // Enemy Mechs guarding the Mech Base
            GroupAddObject(GroupObjectId(22),eve_defense_01);
            GroupAddObject(GroupObjectId(22),eve_defense_02);
            //GroupAddObject(GroupObjectId(22),eve_defense_03);
            GroupAddObject(GroupObjectId(22),eve_defense_04);
            GroupAddObject(GroupObjectId(22),eve_defense_05);
            GroupAddObject(GroupObjectId(22),eve_defense_06);
            GroupAddObject(GroupObjectId(22),eve_tank_01);
            GroupAddObject(GroupObjectId(22),eve_tank_02);
            GroupAddObject(GroupObjectId(22),eve_tank_03);
            GroupAddObject(GroupObjectId(22),eve_tank_04);
            GroupAddObject(GroupObjectId(22),eve_tank_05);
            GroupAddObject(GroupObjectId(22),eve_tank_06);
            GroupAddObject(GroupObjectId(22),etu_turret_1);
            GroupAddObject(GroupObjectId(22),etu_turret_2);
            GroupAddObject(GroupObjectId(22),etu_turret_3);
            GroupAddObject(GroupObjectId(22),etu_turret_4);
            GroupAddObject(GroupObjectId(22),etu_turret_5);
            GroupAddObject(GroupObjectId(22),etu_turret_6);
            GroupAddObject(GroupObjectId(22),etu_turret_7);
            GroupAddObject(GroupObjectId(22),etu_turret_8);

            // Rebel-side Mechs
            GroupAddObject(GroupObjectId(31),eve_defense_01);
            GroupAddObject(GroupObjectId(31),eve_defense_02);
            //GroupAddObject(GroupObjectId(31),eve_defense_03);
            GroupAddObject(GroupObjectId(31),eve_tank_02);
            GroupAddObject(GroupObjectId(31),eve_tank_05);
            GroupAddObject(GroupObjectId(31),eve_tank_06);
            SetGroupAI(groupobjectid(31),GROUPAI_MOODSQUAD);
            
            // PC-side Mechs
            GroupAddObject(GroupObjectId(32),eve_defense_04);
            GroupAddObject(GroupObjectId(32),eve_defense_05);
            GroupAddObject(GroupObjectId(32),eve_defense_06);
            GroupAddObject(GroupObjectId(32),eve_tank_01);
            GroupAddObject(GroupObjectId(32),eve_tank_03);
            GroupAddObject(GroupObjectId(32),eve_tank_04);
            SetGroupAI(groupobjectid(32),GROUPAI_MOODSQUAD);
            
            // Enemy Assault Lance come in to reinforce
            GroupAddObject(GroupObjectId(23),eve_reinforce_01);
            GroupAddObject(GroupObjectId(23),eve_reinforce_02);
            GroupAddObject(GroupObjectId(23),eve_reinforce_03);
            //GroupAddObject(GroupObjectId(23),eve_reinforce_04);
            SetGroupAI(groupobjectid(23),GROUPAI_MOODSQUAD);
            
            // Turrets on the PC Side
            GroupAddObject(GroupObjectId(24),etu_bturret_01);
            GroupAddObject(GroupObjectId(24),etu_bturret_02);
            GroupAddObject(GroupObjectId(24),etu_bturret_06);

            // Enemy Mech Base
            GroupAddObject(GroupObjectId(30),ebu_mech_hangar);
            GroupAddObject(GroupObjectId(30),ebu_base_hq);

		    cinema_cut = 0; 						
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	

        REBEL_FRIEND = GetEventFlag("REBEL_FRIEND");        // This will later be a meta-mission boolean
        
        if (REBEL_FRIEND) then      // If the rebels are your friends, then they will be allies
            SetAlignment(GroupObjectID(10),friendly);
        endif;
        
		setactivationdistance(1200);
        
        setradarnav(ena_Nav_Alpha,1);

        revealnavpoint(ena_Nav_Alpha);
        setnavpoint(ena_Nav_Alpha);

        revealobjective(eob_objective1);
        revealobjective(eob_objective2);
        
//        settargetdesirability(playermech,-1);
        
        checkrange=150;
        StartMusic("Music\DFA",3.0,3.5,70);

//        SetCompositingEnabled(False);
        playerai(epl_player0,true);		


        Wimpy = GetDifficulty;

        // In easy mode take out the two powered down base 'Mechs
        If (wimpy == EASY_DIFFICULTY) then
            //Destroy(eve_defense_03);
            Destroy(eve_defense_04);
            //Destroy(eve_reinforce_04);
            Destroy(etu_turret_7);
            Destroy(etu_turret_4);
            Destroy(etu_turret_2);
            Destroy(etu_bturret_02);
        endif;
            
        trans opening;

//	    trans sit;

endstate;

	
state deadState;
	code

endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

    if (not ismusicplaying) then
    	StartMusic("Music\urbanambloop",0.0,3.0,40);
    endif;

// Audio Triggers

    If (not GetGlobalTrigger(2)) then   // Inbound patrol msg
        if (iswithin(playermech,GroupObjectID(21),900)) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Inbound",TALKER_ERI);
            SetGlobalTrigger(2,true);
        endif;
    endif;

    If (not GetGlobalTrigger(3)) then   // First patrol dead
        if (isdead(GroupObjectID(21))) then
            if (PlayLancemateSound("VOX\OP4\MI3\Eliminated",1)) then
                PlayVoiceOverOnce("VOX\OP4\MI3\Eliminated",TALKER_BAD);
            endif;
            SetGlobalTrigger(3,true);
        endif;
    endif;

    If (not GetGlobalTrigger(4)) then   // Eric warns about turrets
        if (iswithin(PlayerMech,GroupObjectID(24),900)) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Turrets",TALKER_ERI);
            StartTimer(3);              // Timer 2 is for when the rooftop messages pop up
            SetGlobalTrigger(4,true);
        endif;
    endif;
    
    If (not GetGlobalTrigger(5)) then   // First alert that rebels are attacking
        if (timegreater(1,45)) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Rebel",TALKER_BAD);
            SetGlobalTrigger(5,true);
        endif;
    endif;
    
    If (not GetGlobalTrigger(6)) then   // Rebels are now engaged messages
        if (isshot(GroupObjectID(10))) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Intercept",TALKER_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI3\Intercept",TALKER_ERI);
            if (REBEL_FRIEND) then
                PlayLancemateSound("VOX\OP4\MI3\Intercept",1);
            else
                PlayVoiceOverOnce("VOX\OP4\MI3\Intercept2",TALKER_BAD);
                PlayVoiceOverOnce("VOX\OP4\MI3\Intercept2",TALKER_ERI);
            endif;
            SetGlobalTrigger(6,true);
        endif;
    endif;

    If (not GetGlobalTrigger(7)) then   // Turrets on the rooftops messages
        if (timegreater(3,35)) then
            PlayLancemateSound("VOX\OP4\MI3\Rooftops",1);
            SetGlobalTrigger(7,true);
        endif;
    endif;
    
    If (not GetGlobalTrigger(8)) then   // Inside their perimeter
        if (iswithin(playermech,ena_nav_alpha,600)) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Engaging",TALKER_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI3\Tactical",TALKER_BAD);
            SetGlobalTrigger(8,true);
        endif;
    endif;
    
    If (not GetGlobalTrigger(9)) then   // First fleeing rebel message
        if (fleeing==1) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Flee",TALKER_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI3\Flee",TALKER_BAD);
            SetGlobalTrigger(9,true);
        endif;
    endif;

    If (not GetGlobalTrigger(9)) then   // First fleeing rebel message
        if (fleeing==1) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Flee",TALKER_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI3\Flee",TALKER_BAD);
            SetGlobalTrigger(9,true);
        endif;
    endif;

    If (not GetGlobalTrigger(10)) then   // Second fleeing rebel message
        if (fleeing==2) then
            PlayLancemateSound("VOX\OP4\MI3\Flee",1);
            SetGlobalTrigger(10,true);
        endif;
    endif;

    If (not GetGlobalTrigger(11)) then   // All rebels die message
        if (GroupAllDead(GroupObjectID(10))) then
            PlayVoiceOverOnce("VOX\generic\RebelFlee",TALKER_BAD);
            SetGlobalTrigger(11,true);
        endif;
    endif;

// Triggers

    if (not EnterTheRebel) then         // This controls when the rebel forces come from the north
        if (groupnumdead(GroupObjectID(20))>=2) then
            if (timegreater(1,120)) then    // was 180
                EnterTheRebel=true;
            endif;
        endif;
    endif;
    
    if (not EnterTheRebel) then         // This controls when the rebel forces come from the north
        if (iswithin(playermech,eve_rebel_01,1300)) then
            EnterTheRebel=true;
        endif;
    endif;
    
    If (not GetGlobalTrigger(1)) then   // This controls when the Dresari Assault lance comes from the south
        if (not EnterTheEnemy) then
            if (groupnumdead(GroupObjectID(20))>=4) then
                if (obj1) then
                    StartTimer(2);
                    EnterTheEnemy=true;
                    SetGlobalTrigger(1,true);
                endif;
            endif;
        endif;
    endif;
    
    if (not GetGlobalTrigger(12)) then
        if (timegreater(2,2)) then     // was 90
            //if (groupnumdead(GroupObjectID(20))>=4) then
            PlayVoiceOverOnce("VOX\OP4\MI3\Assault",TALKER_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI3\Assault",TALKER_ERI);
            PlayLancemateSound("VOX\OP4\MI3\Assault",1);
            revealobjective(eob_objective3);
            SetGlobalTrigger(12,true);
            killtimer(2);
        endif;
    endif;
	
// Reveal the objectives
// Obj 1 - Eliminate the Mech Base
    
    if (not obj1) then
        if (GroupAllDead(GroupObjectID(30))) then
            SuccessObjective(eob_objective1);
            obj1=true;
            MissionCounter=MissionCounter+1;
        endif;
    endif;
    
// Obj 2 - Destroy the Mech Base Guards

    if (not obj2) then
        if (GroupAllDead(GroupObjectID(22))) then
            SuccessObjective(eob_objective2);
            obj2=true;
            MissionCounter=MissionCounter+1;
            PlayVoiceOverOnce("VOX\OP4\MI3\Late",TALKER_BAD);
        endif;
    endif;
    
// Obj 3 - Destroy the Assault Patrol

    if (not obj3) then
        if (GroupAllDead(GroupObjectID(23))) then
            SuccessObjective(eob_objective3);
            obj3=true;
            MissionCounter=MissionCounter+1;
        endif;
    endif;

// Win Mission!

    if (MissionCounter==3) then

      // Salvage Added by Ferret 7/28/01         
        AddWeaponSalvage("LargeXPulseLaser",1);
        AddWeaponSalvage("ClanLRM20",1);
        AddWeaponSalvage("PPC",2);
        AddWeaponSalvage("Gauss",1);
        AddWeaponSalvage("ClanUltraAC10",1);
        AddWeaponSalvage("ClanLRM10",3);
        AddWeaponSalvage("ERSmallLaser",6);

        AddMechInstanceSalvage("Mad Cat MKII");
        AddMechInstanceSalvage("Loki");
        AddMechInstanceSalvage("Argus");
        
        AddWeaponToMarket("ClanUltraAC5",2);
        AddWeaponToMarket("LargeXPulseLaser",2);
        AddWeaponToMarket("ClanGauss",1);
        
        AddMechToMarket("Mad Cat MKII");
      //        
        
        trans won;
    endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceover("VOX\generic\EriDestroyedKnight",talker_Bad);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,13);
		Trans Lost;
	endif;

// Betty speaks when reaching Nav Point

    if (not alphareached) then
	    if (isnavrevealed(ena_nav_alpha)) then
		    if (iswithin(playermech,ena_nav_alpha,checkrange)) then
			    playvoiceover("VO\generic\navalpha",talker_Bet);
			    alphareached = true;
                // setnavpoint(ena_nav_beta);
		    endif;
	    endif;
    endif;
				
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;

	code
	
		if (cinemaskip) then
			cinema_cut = 0;
			resettimer (gti_Timer_21);
			killtimer(gti_timer_21);
            starttimer(1);
            resettimer(1);
            playerai(epl_player0,false);
			cinemaend;
            SetCompositingEnabled(True);
			startup(GroupObjectID(21));
            KillVoiceOvers;
            //PlayVoiceOverOnce("VO\Generic\Startup2",Talker_Bet);
            //PlayVoiceOverOnce("VO\Generic\Startup3",Talker_Bet);
            //PlayVoiceOverOnce("VO\Generic\Startup6",Talker_Bet);
			trans sit;	
	endif;
			
switch (cinema_cut)
			
			case 0:
    			
                SetCompositingEnabled(False);
                cinemastart;
                
                shutdown(GroupObjectID(21));        // Shutdown the interceptors, they will attack once the movie ends
    			setactivecamera(eca_cameraship2);
    			Fadetoblack(0.0);
    
    			Targetfollowobject(playermech);
    			camerafollowobject(playermech);
    
    			targetoffset(0.0,4.0,0.0,0.0,false);
    			Cameraoffset(-140.0,49.0,4.0,0.0,false);
    
    			Cameraoffset(-90.0,35.0,12.0,9.0,false);
    
    			startTimer (gti_Timer_21);
    			resettimer (gti_Timer_21);
                SetCameraFootShake(playermech,100);
    			FadeFromBlack(5.0);
                PlayVoiceOverOnce("VOX\OP4\MI3\Intro",TALKER_BAD);
                PlayVoiceOverOnce("VOX\OP4\MI3\Intro",TALKER_ERI);
                cinema_cut = cinema_cut + 1;
			endcase;

			case 1:
				
				if (timegreater(gti_Timer_21,8)) then
                    setactivecamera(eca_cameraship2);
					Targetfollowobject(playermech);
					camerafollowobject(playermech);
					targetoffset(0.0,8.0,0.0,0.0,false);
					Cameraoffset(-130.0,49.0,35.0,0.0,true);
					Cameraoffset(-90.0,35.0,60.0,17.0,true);
					Fadefromblack(2.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 2;  // Kill Case 2 transition
				endif;
				
			endcase;
					
		    case 2:
				
				if (timegreater(gti_Timer_21,8)) then
					Targetfollowobject(playermech);
					camerafollowobject(playermech);
					targetoffset(0.0,8.0,0.0,0.0,false);
					Cameraoffset(-130.0,49.0,-35.0,0.0,true);
					Cameraoffset(-90.0,35.0,-60.0,17.0,true);
					Fadefromblack(2.0);
					resetTimer (gti_Timer_21);
					cinema_cut = cinema_cut + 1;
				endif;
				
			endcase;
	
			case 3:
				
				if (timegreater(gti_Timer_21,12)) then
					cinema_cut = 0;
					resettimer (gti_Timer_21);
					killtimer(gti_timer_21);
                    starttimer(1);
                    resettimer(1);
                    playerai(epl_player0,false);
					cinemaend;
                    SetCompositingEnabled(True);
        			startup(GroupObjectID(21));
    				PlayVoiceOverOnce("VO\Generic\Startup2",Talker_Bet);
    				PlayVoiceOverOnce("VO\Generic\Startup3",Talker_Bet);
    				PlayVoiceOverOnce("VO\Generic\Startup6",Talker_Bet);
                    trans sit;
				endif;
				
			endcase;
			
		endswitch;
	
	
endstate;

state won;

		code
		
		if (cinemaskip) then
//			cinema_cut = 0;
			playerai(epl_player0,false);
			Endmission(true,1);
			KillVoiceOvers;
			cinemaend;
		endif;
		
		switch cinema_cut
		
			case 0:	
				SetCompositingEnabled(False);
                CinemaStart;
                endmission(true,14);
                StartMusic("Music\Victory",3.0,3.5,55);
                PlayVoiceOverOnce("VOX\OP4\MI3\Victory",TALKER_ERI);
                cinema_cut = cinema_cut + 1;
				SecondCin = TRUE;
				playerai(epl_player0,true);
				SetTargetDesirability(playermech,-1);
				ClearMoveOrder(playermech);
				setactivecamera (eca_cameraship2);
				Fadefromblack(0.0);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(60.0,80.0,-150.0,0.0,false);
				Cameraoffset(0.0,20.0,50.0,15.0,false);
				FadeFromBlack(5.0);
                StartTimer(21);
                ResetTimer(21);
				Cinema_cut=Cinema_cut+1;
			endcase;
			
			case 1:
				
				if (timegreater(gti_Timer_21,15)) then
//					cinema_cut = 0;
					resettimer (gti_Timer_21);
					killtimer(gti_timer_21);
					playerai(epl_player0,false);
					FadeToBlack(1.0);
					cinemaend;
                    EndMission(true,1);	
				endif;
				
			endcase;
			
		endswitch;
	
endstate;


state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

endfsm.
//******************************************************************

