
//*********************************************************************************

fsm ruin2_checkpointcheckout : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer     playermech;
		
        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
        static integer      checkrange;         // How far from the Nav Point you have to be                                            
                                                //
        eternal boolean     obj1;               // This indicates whether Obj 1 was completed
        eternal boolean     obj2;               // This indicates whether Obj 2 was completed
        eternal boolean     obj3;               // This indicates whether Obj 3 was completed
        eternal boolean     obj4;               // This indicates whether Obj 4 was completed
        eternal boolean     obj5;               // This indicates whether Obj 5 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        static boolean      obj2revealed;       // This indicates that Obj 2 has been revealed
        static boolean      obj3revealed;       // This indicates that Obj 3 has been revealed
        static boolean      obj4revealed;       // This indicates that Obj 4 has been revealed
        //    
		
		static integer	    cinema_cut;
        static integer      wimpy;              // For the easy setting
        static integer      clearedpoint;       // The number of Checkpoints cleared out
        eternal boolean     REBEL_FRIEND;       // This will be a meta-mission variable later
        eternal boolean     SQUIRE_FRIEND;      // This will be a meta-mission variable later
        eternal boolean     EnterSquire;        // This controls when the Rebel Squire comes to help out the gang
        eternal boolean     tankergo;           // This controls when the tank ambush is sprung
		
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

		    playermech = getplayervehicle(epl_player0);	
 	
			GroupAddObject(GroupObjectId(1),playermech);
            if (GetLancemate(1)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(1));
            endif;

            if (GetLancemate(2)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(2));
            endif;

            if (GetLancemate(3)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(3));
            endif;
 
            // Nav Alpha enemies           
            GroupAddObject(GroupObjectId(20),etu_aturret_1);
            GroupAddObject(GroupObjectId(20),etu_aturret_2);
            GroupAddObject(GroupObjectId(20),etu_aturret_3);
            //GroupAddObject(GroupObjectId(20),eve_amech_1);
            GroupAddObject(GroupObjectId(20),eve_amech_2);
            GroupAddObject(GroupObjectId(20),eve_amech_3);
            GroupAddObject(GroupObjectId(20),eve_atank_1);
            GroupAddObject(GroupObjectId(20),eve_atank_2);
            //GroupAddObject(GroupObjectId(20),eve_atank_3);
            SetGroupAI(GroupObjectID(20),GROUPAI_MOODSQUAD);

            // Nav Beta enemies           
            GroupAddObject(GroupObjectId(21),etu_bturret_1);
            GroupAddObject(GroupObjectId(21),etu_bturret_2);
            GroupAddObject(GroupObjectId(21),eve_bmech_1);
            GroupAddObject(GroupObjectId(21),eve_bmech_2);
            //GroupAddObject(GroupObjectId(21),eve_btank_1);
            GroupAddObject(GroupObjectId(21),eve_btank_2);
            SetGroupAI(GroupObjectID(21),GROUPAI_MOODSQUAD);

            // Nav Gamma enemies           
            GroupAddObject(GroupObjectId(22),etu_gturret_1);
            GroupAddObject(GroupObjectId(22),etu_gturret_2);
            GroupAddObject(GroupObjectId(22),etu_gturret_3);
            GroupAddObject(GroupObjectId(22),eve_gmech_1);
            GroupAddObject(GroupObjectId(22),eve_gmech_2);
            //GroupAddObject(GroupObjectId(22),eve_gtank_1);
            //GroupAddObject(GroupObjectId(22),eve_gtank_2);
            SetGroupAI(GroupObjectID(22),GROUPAI_MOODSQUAD);
 		
            // Nav Delta enemies
            GroupAddObject(GroupObjectId(23),etu_dturret_1);
            GroupAddObject(GroupObjectId(23),etu_dturret_2);
            GroupAddObject(GroupObjectId(23),etu_dturret_3);
            GroupAddObject(GroupObjectId(23),etu_dturret_4);
            GroupAddObject(GroupObjectId(23),eve_dmech_1);
            GroupAddObject(GroupObjectId(23),eve_dmech_2);
            GroupAddObject(GroupObjectId(23),eve_dmech_3);
            GroupAddObject(GroupObjectId(23),eve_dtank_1);
            SetGroupAI(GroupObjectID(23),GROUPAI_MOODSQUAD);

            // Tank Ambush between Gamma and Delta
            GroupAddObject(GroupObjectId(24),eve_tank_01);
            GroupAddObject(GroupObjectId(24),eve_tank_02);
            GroupAddObject(GroupObjectId(24),eve_tank_03);
            GroupAddObject(GroupObjectId(24),eve_tank_04);
            GroupAddObject(GroupObjectId(24),eve_tank_05);
            GroupAddObject(GroupObjectId(24),eve_tank_06);
            
    		cinema_cut = 0; 						
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state StartState;

	code	
	    //playsound(eso_start);
		setactivationdistance(1200);
		// settargetdesirability(playermech,-1);

        setradarnav(ena_Nav_Alpha,1);
        setradarnav(ena_Nav_Beta,2);
        setradarnav(ena_Nav_Gamma,3);
        setradarnav(ena_Nav_Delta,4);

        revealnavpoint(ena_Nav_Alpha);
        setnavpoint(ena_Nav_Alpha);

        revealobjective(eob_objective1);
        revealobjective(eob_objective2);
        revealobjective(eob_objective3);
        revealobjective(eob_objective4);
        
        // settargetdesirability(GroupObjectID(21),-1);
        
        checkrange=150;
        SQUIRE_FRIEND = GetEventFlag("SQUIRE_FRIEND");        // This will later be a metamission variable
        REBEL_FRIEND = GetEventFlag("REBEL_FRIEND");        // This will later be a metamission variable

        SetCompositingEnabled(False);
        CinemaStart;
        StartMusic("Music\ActionPack",3.0,3.5,35);

        Wimpy = GetDifficulty;

        // In easy mode take out the two powered down base 'Mechs
        If (wimpy == EASY_DIFFICULTY) then
            Shutdown(eve_gmech_2);
            TeleporttoHell(eve_gmech_2);
            Destroy(eve_gmech_2);
            Shutdown(eve_dmech_2);
            TeleporttoHell(eve_dmech_2);
            GroupRemoveObject(GroupObjectId(23),eve_dmech_2);
            Destroy(eve_dmech_2);
            Destroy(etu_aturret_2);
            Destroy(etu_gturret_2);
            Destroy(etu_dturret_2);
        endif;
            
        trans opening;
            
	    // trans sit;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

    if (not ismusicplaying) then
    	StartMusic("Music\urbanambloop",0.0,3.0,40);
    endif;
	
// Audio Triggers

    if (not GetGlobalTrigger(2)) then       // This is a line when the player gets near Nav Alpha
        if (iswithin(playermech,ena_nav_alpha,600)) then
            PlayVoiceOverOnce("VOX\OP4\MI2\Credentials",talker_ERI);
            SetGlobalTrigger(2,true);
            StartTimer(2);      // Timer for Eric's comment on the Alpha Demolisher
        endif;
    endif;

    if (not GetGlobalTrigger(3)) then       // A comment about the turrets
        if (WhoShot(GroupObjectID(1))==etu_aturret_1) then
            PlayLancemateSound("VOX\OP4\MI2\Turret",1);
            SetGlobalTrigger(3,true);
        else
            if (WhoShot(GroupObjectID(1))==etu_aturret_2) then
                PlayLancemateSound("VOX\OP4\MI2\Turret",1);
                SetGlobalTrigger(3,true);
            endif;
        endif;
    endif;

    if (not GetGlobalTrigger(4)) then       // Demolisher comment at Alpha
        if (not isdead(eve_atank_2)) then
            if (timegreater(2,15)) then
                PlayVoiceOverOnce("VOX\OP4\MI2\Demolisher",talker_ERI);
                SetGlobalTrigger(4,true);
                KillTimer(2);
            endif;
        endif;
    endif;

    if (not GetGlobalTrigger(5)) then       // Nav Beta intro
        if (iswithin(playermech,ena_nav_beta,550)) then
            PlayVoiceOverOnce("VOX\OP4\MI2\MoreSame",talker_ERI);
            SetGlobalTrigger(5,true);
            StartTimer(2);  // The timer is being recycled for the next block
        endif;
    endif;

    if (GetGlobalTrigger(5)) then           // This is Olsen griping about close in fighting
        if (not GetGlobalTrigger(8)) then
            If (timegreater(2,11)) then            
                If (PlayLancemateSound("VOX\OP4\MI2\Quarters",1)) then
                    PlayVoiceOverOnce("VOX\OP4\MI2\Quarters",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Quarters2",talker_OLS);
                endif;
                SetGlobalTrigger(8,true);
                KillTimer(2);
            endif;
        endif;
    endif;      

    if (not GetGlobalTrigger(6)) then       // Nav Delta info
        if (timegreater(3,24)) then
            PlayVoiceOverOnce("VOX\OP4\MI2\Gamma",talker_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI2\Gamma2",talker_ERI);
            SetGlobalTrigger(6,true);
        endif;
    endif;

    if (not GetGlobalTrigger(7)) then       // Nav Delta engage
        if (whoshot(GroupObjectID(23))==playermech) then
            PlayVoiceOverOnce("VOX\OP4\MI2\Engaging",talker_ERI);
            SetGlobalTrigger(7,true);
        endif;
    endif;

// Mission Triggers

    if (not EnterSquire) then       // Your rebel friend Squire will help
        if (SQUIRE_FRIEND) then
            if (GetGlobalTrigger(6)) then
                if (iswithin(playermech,ena_nav_delta,600)) then
                    EnterSquire = true;
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission",talker_SQU);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission2",talker_SQU);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission2",talker_ERI);
                endif;
            endif;
        endif;
    endif;

    if (not EnterSquire) then
        if (SQUIRE_FRIEND) then
            if (GetGlobalTrigger(6)) then
                if (iswithin(playermech,ena_squirenav,600)) then
                    EnterSquire = true;
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission",talker_SQU);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission2",talker_SQU);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Permission2",talker_ERI);
                endif;
            endif;
        endif;
    endif;
    
    if (not tankergo) then          // This controls when the tankers wake up and spring into action
        if (iswithin(playermech,ena_tankambush,500)) then
            tankergo = true;
        endif;
    endif;

    if (not tankergo) then          // This controls when the tankers wake up and spring into action
        if (isshot(GroupObjectID(24))) then
            tankergo = true;
        endif;
    endif;

// Obj 1 - Clear Nav Alpha

    if (not obj1) then
        if (GroupAllDead(GroupObjectID(20))) then
            obj1=true;
            successobjective(eob_objective1);
            PlayVoiceOverOnce("VOX\OP4\MI2\Alpha",talker_ERI);
            clearedpoint=clearedpoint+1;
            revealnavpoint(ena_Nav_Beta);
            setnavpoint(ena_Nav_Beta);
        endif;
    endif;
    
// Obj 2 - Clear Nav Beta

    if (not obj2) then
        if (GroupAllDead(GroupObjectID(21))) then
            obj2=true;
            successobjective(eob_objective2);
            clearedpoint=clearedpoint+1;
            StartMusic("Music\Sander",3.5,3.0,60);
            PlayVoiceOverOnce("VOX\OP4\MI2\Beta",talker_ERI);
            if (REBEL_FRIEND) then
                PlayVoiceOverOnce("VOX\OP4\MI2\Beta",talker_BAD);
                if (PlayLancemateSound("VOX\OP4\MI2\Beta",1)) then
                    PlayVoiceOverOnce("VOX\OP4\MI2\Beta2",talker_ERI);
                    PlayLancemateSound("VOX\OP4\MI2\Beta2",1);
                endif;
            endif;
            revealnavpoint(ena_Nav_Gamma);
            setnavpoint(ena_Nav_Gamma);
        endif;        
    endif;
    
// Obj 3 - Clear Nav Gamma

    if (not obj3) then
        if (GroupAllDead(GroupObjectID(22))) then
            obj3=true;
            successobjective(eob_objective3);
            clearedpoint=clearedpoint+1;
            PlayVoiceOverOnce("VOX\OP4\MI2\Gamma",talker_ERI);
            Starttimer(3);  // This is the timer for info on Delta
            revealnavpoint(ena_Nav_Delta);
            setnavpoint(ena_Nav_Delta);
        endif;
    endif;

// Obj 4 - Clear Nav Delta

    if (not obj4) then
        if (GroupAllDead(GroupObjectID(23))) then
            obj4=true;
            successobjective(eob_objective4);
            clearedpoint=clearedpoint+1;

            if (SQUIRE_FRIEND) then
                if (not isdead(eve_Squire)) then
                    PlayVoiceOverOnce("VOX\OP4\MI2\Gamma",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Thanks",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI2\Thanks",talker_SQU);
                endif;
            endif;
        endif;
    endif;

// Victory! - Need to clear all four nav points

    if (clearedpoint==4) then
		enableglobalinvulnerable;
		settargetdesirability(playermech,-1);
        resettimer(gti_timer_28);
		starttimer(gti_timer_28);
        trans waitforcinema;
    endif;
    
//Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\EriDestroyedKnight3",talker_BAD);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
		trans lostdead;
	endif;
		
// Betty speaks when reaching Nav Point

    if (not alphareached) then
	    if (isnavrevealed(ena_nav_alpha)) then
		    if (iswithin(playermech,ena_nav_alpha,checkrange)) then
			    playvoiceover("VO\generic\navalpha",talker_Bet);
			    alphareached = true;
		    endif;
	    endif;
    endif;

    if (not betareached) then
    	if (isnavrevealed(ena_nav_beta)) then
    		if (iswithin(playermech,ena_nav_beta,checkrange)) then
    			playvoiceover("VO\generic\navbeta",talker_Bet);
    			betareached = true;
    		endif;
    	endif;
    endif;
    
    if (not gammareached) then
    	if (isnavrevealed(ena_nav_gamma)) then
    		if (iswithin(playermech,ena_nav_gamma,checkrange)) then
    			playvoiceover("VO\generic\navgamma",talker_Bet);
    			gammareached = true;
    		endif;
    	endif;
    endif;
    
    if (not deltareached) then
    	if (isnavrevealed(ena_nav_delta)) then
    		if (iswithin(playermech,ena_nav_delta,checkrange)) then
    			playvoiceover("VO\generic\navdelta",talker_Bet);
    			deltareached = true;
    		endif;
    	endif;
    endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state waitforcinema;
		
		code
		
		if (timegreater(gti_timer_28,9)) then
			playerai(epl_player0,true);

      // Salvage Added by Ferret 7/28/01         
        AddWeaponSalvage("AC5",4);
        AddWeaponSalvage("MRM10",4);            

        AddMechInstanceSalvage("Mauler");
        AddMechInstanceSalvage("Mad Cat");
        
        AddWeaponToMarket("ERPPC",1);
        AddWeaponToMarket("ClanLRM20",2);
        
        AddMechToMarket("Daishi");
        
      //
      
            trans won;
		endif;
		
		
endstate;

state opening;

	code

	if (cinemaskip) then
    	KillVoiceOvers;
		cinema_cut = 0;
		KillTimer(gti_timer_21);
		SetGlobalTrigger(0,true);
		playerai(epl_player0,false);
		CinemaEnd;
        SetCompositingEnabled(True);

		trans sit;
    endif;
        
        switch (cinema_cut)
			
			case 0:
                SetCompositingEnabled(False);
                SetActiveCamera(eca_CameraShip);

                PlayVoiceOverOnce("VOX\OP4\MI2\Intro",talker_BAD);
                if (REBEL_FRIEND) then
                    PlayVoiceOverOnce("VOX\OP4\MI2\Intro2",talker_BAD);
                    PlayLancemateSound("VOX\OP4\MI2\Intro",1);
                endif;
                
                PlayVoiceOverOnce("VOX\OP4\MI2\Crush",talker_ERI);
                
                FadeToBlack(0.0);
                starttimer(1);
                resettimer(1);

                Targetfollowobject(playermech);
                Camerafollowobject(playermech);
                Targetoffset(0.0,4.0,0.0,0.0,false);
                Cameraoffset(10.0,4.0,-150.0,0.0,false);
                Cameraoffset(-120.0,60.0,50.0,6.0,false);
                FadeFromBlack(3.0);
                
                cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
                if (timegreater(1,8)) then
                    TargetFollowObject(ena_Nav_Alpha);
                    CameraFollowObject(ena_Nav_Alpha);
                    TargetOffset(0.0,0.0,0.0,0.0,false);
                    TargetOffset(30.0,0.0,200.0,12.0,false);
                    CameraOffset(300.0,50.0,0.0,0.0,true);
                    CameraOffset(50.0,175.0,-125.0,12.0,true);
                    // CameraOffset(0.0,200.0,-150.0,12.0,true);
                    
                    // CameraOffset(200.0,200.0,0.0,0.0,true);
                    // CameraOffset(-200.0,100.0,200.0,14.0,true);
                    cinema_cut = cinema_cut + 1;
                    resettimer(1);
                endif;
			endcase;
			
			case 2:
                // Cameraoffset(0.0,5.0,0.0,0.0,true);
                // Cameraoffset(100.0,5.0,0.0,14.0,true);
                if (timegreater(1,12)) then
                    TargetOffset(30.0,0.0,200.0,0.0,false);
                    TargetOffset(0.0,0.0,0.0,6.0,false);
                    CameraOffset(50.0,175.0,-125.0,0.0,true);
                    // CameraOffset(0.0,200.0,-150.0,0.0,true);
                    CameraOffset(0.0,50.0,-350.0,6.0,true);
                    cinema_cut = cinema_cut + 1;
                    resettimer(1);
                endif;
			endcase;

			case 3:
                if (timegreater(1,6)) then
                    cinema_cut = cinema_cut + 1;
                    resettimer(1);
                    KillTimer(gti_timer_21);
                endif;
			endcase;
			
            case 4:
                KillVoiceOvers;
                cinema_cut = 0;
                KillTimer(gti_timer_21);
                SetGlobalTrigger(0,true);
                playerai(epl_player0,false);
                CinemaEnd;
                SetCompositingEnabled(True);
                trans sit;
			endcase;
			
		endswitch;

	
	
endstate;

state won;

		code
		
	if (cinemaskip) then
			cinema_cut = 20;
			resettimer (gti_Timer_21);
			killtimer(gti_timer_21);
			endmission(true,1);	
	endif;
		
		switch cinema_cut
		
			case 0:	
				SetCompositingEnabled(False);
                CinemaStart;
				setactivecamera (eca_cameraship);
				//Fadetoblack(7.0);
				Targetfollowobject(playermech);
				Camerafollowobject(playermech);
				Targetoffset(0.0,4.0,0.0,0.0,false);
				Cameraoffset(-30.0,125.0,75.0,0.0,false);
				Cameraoffset(60.0,20.0,25.0,20.0,false);
                StartMusic("Music\Victory",3.0,3.5,50);
                endmission(true,20);
                PlayVoiceOverOnce("VOX\OP4\MI2\Victory",talker_BAD);
                PlayVoiceOverOnce("VOX\OP4\MI2\Victory",talker_ERI);
			    Cinema_Cut = Cinema_Cut+1;
			endcase;
			
			case 1:
			endcase;
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

state lostdead;
    code

endstate;

endfsm.
//******************************************************************

