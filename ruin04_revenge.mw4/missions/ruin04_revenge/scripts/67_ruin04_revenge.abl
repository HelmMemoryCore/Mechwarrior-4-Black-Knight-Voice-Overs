
//*********************************************************************************

fsm revenge : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer     playermech;
		
        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
        static integer      checkrange;         // How far from the Nav Point you have to be                                            
                                                //
        eternal boolean     obj1;               // This indicates whether Obj 1 was completed
        eternal boolean     obj2;               // This indicates whether Obj 2 was completed
        eternal boolean     obj3;               // This indicates whether Obj 3 was completed
        eternal boolean     obj4;               // This indicates whether Obj 4 was completed
        eternal boolean     obj5;               // This indicates whether Obj 5 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        static boolean      obj2revealed;       // This indicates that Obj 2 has been revealed
        static boolean      obj3revealed;       // This indicates that Obj 3 has been revealed
        static boolean      obj4revealed;       // This indicates that Obj 4 has been revealed
        //    
		
		static integer	    cinema_cut;
        static integer      wimpy;              // For the easy setting
        static integer      ian;                // The shortcute for Ian's Mech
        static integer      missioncounter;     // Used to calculcate whether the mission is won or not
        eternal boolean     ambush1;            // The first team comes out of hiding
        eternal boolean     ambush2;            // The first team comes out of hiding
        static integer      lancedied;          // Keeps track of lancemates death for Ian VO
        static locpoint     tempnav;            // Used for making things teleport places
        static boolean      REBEL_FRIEND;       // Temp global variable
		
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

		 playermech = getplayervehicle(epl_player0);	
         ian        = eve_ian_dresari;
 	
			GroupAddObject(GroupObjectId(1),playermech);
            
            if (GetLancemate(1)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(1));
            endif;

            if (GetLancemate(2)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(2));
            endif;

            if (GetLancemate(3)<>NO_UNIT) then
                GroupAddObject(GroupObjectId(1),GetLancemate(3));
            endif;

            // Turrets
            GroupAddObject(GroupObjectId(10),etu_turret_01);
            GroupAddObject(GroupObjectId(10),etu_turret_02);
            GroupAddObject(GroupObjectId(10),etu_turret_03);
            GroupAddObject(GroupObjectId(10),etu_turret_04);
            SetGroupAI(groupobjectid(10),GROUPAI_MOODSQUAD);

			// First Wave of Mechs
            GroupAddObject(GroupObjectId(20),eve_wave1_01);
            GroupAddObject(GroupObjectId(20),eve_wave1_02);
            GroupAddObject(GroupObjectId(20),eve_wave1_03);
            GroupAddObject(GroupObjectId(20),eve_wave2w_01);
            GroupAddObject(GroupObjectId(20),eve_wave2w_02);
            SetGroupAI(groupobjectid(20),GROUPAI_MOODSQUAD);

            // Second Wave of Mechs
            GroupAddObject(GroupObjectId(21),eve_wave3_01);
            GroupAddObject(GroupObjectId(21),eve_wave3_02);
            GroupAddObject(GroupObjectId(21),eve_wave3_03);
            SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);
            
            // Ian Dressari and his boys
            GroupAddObject(GroupObjectId(30),ian);
            GroupAddObject(GroupObjectId(30),eve_Jen_McQuarrie);
            GroupAddObject(GroupObjectId(30),eve_Jules_Gonzalez);
            SetGroupAI(groupobjectid(30),GROUPAI_MOODSQUAD);

            // Non Ian Mechs - Just for an objective
            GroupAddObject(GroupObjectId(40),eve_Jen_McQuarrie);
            GroupAddObject(GroupObjectId(40),eve_Jules_Gonzalez);
            GroupAddObject(GroupObjectId(40),eve_wave1_01);
            GroupAddObject(GroupObjectId(40),eve_wave1_02);
            GroupAddObject(GroupObjectId(40),eve_wave1_03);
            GroupAddObject(GroupObjectId(40),eve_wave2w_01);
            GroupAddObject(GroupObjectId(40),eve_wave2w_02);
            GroupAddObject(GroupObjectId(40),eve_wave3_01);
            GroupAddObject(GroupObjectId(40),eve_wave3_02);
            GroupAddObject(GroupObjectId(40),eve_wave3_03);
            
        cinema_cut = 0; 						
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
		setactivationdistance(1200);

        setradarnav(ena_Nav_Alpha,1);
        setradarnav(ena_Nav_Beta,2);
        revealnavpoint(ena_Nav_Alpha);
        setnavpoint(ena_Nav_Alpha);

        REBEL_FRIEND = GetEventFlag("REBEL_FRIEND");

        revealobjective(eob_objective1);
        revealobjective(eob_objective2);
//        revealobjective(eob_objective3);
        
//        settargetdesirability(playermech,-1);
        
        checkrange=150;

//        SetCompositingEnabled(False);
//        CinemaStart;
//        trans opening;

        StartMusic("Music\SteinerDefeat",3.0,3.5,70);

        Wimpy = GetDifficulty;

        // In easy mode take out the two powered down base 'Mechs
        If (wimpy == EASY_DIFFICULTY) then
            GetLocation(ena_dump,tempnav);
            teleport(eve_wave3_02,tempnav);
            SelfDestruct(eve_wave3_02);     // I was using the Destroy command, but this one seems more reliable
            GetLocation(ena_dump,tempnav);
            teleport(eve_wave1_03,tempnav);
            SelfDestruct(eve_wave1_03);
            GetLocation(ena_dump,tempnav);
            teleport(eve_Jen_McQuarrie,tempnav);
            SelfDestruct(eve_Jen_McQuarrie);
        endif;
        
	    trans opening;
        //trans won;

endstate;

	
state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;
	code

    if (not ismusicplaying) then
    	StartMusic("Music\urbanambloop",0.0,3.0,40);
    endif;

// Sound Triggers

    if (not GetGlobalTrigger(3)) then
        if (GroupAllDead(GroupObjectID(20))) then
            SetGlobalTrigger(3,true);
            PlayVoiceOverOnce("VOX\OP4\MI4\Alpha",talker_ERI);
        endif;
    endif;

    if (not GetGlobalTrigger(4)) then       // When one of your lancemates die if Ian is around
        if (not isdead(eve_ian_dresari)) then
            if (obj1) then
                if (GroupNumDead(GroupObjectID(1)) > LanceDied) then
                    PlayVoiceOverOnce("VOX\OP4\MI4\Cockiness",talker_IAN);
                    SetGlobalTrigger(4,true);            
                endif;
            endif;
        endif;
    endif;

    if (not GetGlobalTrigger(5)) then       // When both of Ian's goons die
        if (not isdead(eve_ian_dresari)) then
            if (GroupNumDead(GroupObjectID(30))==2) then
                PlayVoiceOverOnce("VOX\OP4\MI4\Eject",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI4\Eject",talker_IAN);
                SetGlobalTrigger(5,true);            
            endif;
        endif;
    endif;
    
// Triggers
    
    if (not ambush1) then
        if (isshot(GroupObjectID(20))) then
            ambush1=true;
            PlayLancemateSound("VOX\OP4\MI4\Come",1);
            PlayVoiceOverOnce("VOX\OP4\MI4\Come",talker_ERI);
        endif;
    endif;
    
    if (not ambush1) then
        if (iswithin(GroupObjectID(1),GroupObjectID(20),300)) then
            ambush1=true;
            PlayLancemateSound("VOX\OP4\MI4\Powerup",1);
            PlayLancemateSound("VOX\OP4\MI4\Come",1);
            PlayVoiceOverOnce("VOX\OP4\MI4\Come",talker_ERI);
        endif;
    endif;

    if (not ambush2) then
        if (isshot(GroupObjectID(21))) then
            ambush2=true;
            PlayVoiceOverOnce("VOX\OP4\MI4\Scared",talker_ERI);
        endif;
    endif;	

    if (not ambush2) then
        if (iswithin(GroupObjectID(1),GroupObjectID(21),250)) then
            ambush2=true;
            PlayVoiceOverOnce("VOX\OP4\MI4\Scared",talker_ERI);
        endif;
    endif;
    
    if (not GetGlobalTrigger(2)) then       // This is to turn on the turrets
        if (iswithin(playermech,GroupObjectID(10),400)) then
            SetAlignment(GroupObjectID(10),enemy);
            SetGlobalTrigger(2,true);
        else
            if (isshot(GroupObjectID(10))) then
                SetAlignment(GroupObjectID(10),enemy);
                SetGlobalTrigger(2,true);            
            endif;
        endif;
    endif;

// Reveal the objectives

// Objective 1 - Find Ian Dresari

    if (not obj1) then
        if (not iswithin(playermech,ena_HelloIan,1250)) then
            SuccessObjective(eob_objective1);
            RevealObjective(eob_objective3);
            obj1=true;
            LanceDied = GroupNumDead(GroupObjectID(1));
            //If ((GetMechType(playermech))==M_BlackKnight) then
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown",talker_IAN);
            //else
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown2",talker_IAN);
            //endif;
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown2",talker_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown3",talker_IAN);
            PlayLancemateSound("VOX\OP4\MI4\dispatch",1);
        endif;
    endif;

    if (not obj1) then  // If the lancemates get to close to Beta have Ian's croneys show up
        if (iswithin(GroupObjectID(1),ena_nav_beta,400)) then
            SuccessObjective(eob_objective1);
            RevealObjective(eob_objective3);
            obj1=true;
            LanceDied = GroupNumDead(GroupObjectID(1));
            //If ((GetMechType(playermech))==M_BlackKnight) then
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown",talker_IAN);
            //else
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown2",talker_IAN);
            //endif;
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown2",talker_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI4\showdown3",talker_IAN);
            PlayLancemateSound("VOX\OP4\MI4\dispatch",1);
        endif;
    endif;

// Objective 2 - Destroy all Dresari Mechs

    if (not obj2) then
        if (groupalldead(GroupObjectID(40))) then
            SuccessObjective(eob_objective2);
            obj2=true;
            missioncounter=missioncounter+1;
        endif;
    endif;

// Objective 3 - Kill Ian Dresari

    if (not obj3) then
        if (isdead(ian)) then
            SuccessObjective(eob_objective3);
            obj3=true;
            missioncounter=missioncounter+1;
        endif;
    endif;
    
// Mission Won

    if (not GetGlobalTrigger(6)) then       // This is to have a delay before the outro movie
        if (missioncounter==2) then
            SetGlobalTrigger(6,true);
            starttimer(3);
        endif;
    endif;

    if (timegreater(3,5)) then
      
      // Salvage Added by Ferret 7/28/01         
        AddWeaponSalvage("MRM20",2);
        AddWeaponSalvage("MRM40",2);
        AddWeaponSalvage("ERPPC",2);
        AddWeaponSalvage("Gauss",2);
        AddWeaponSalvage("UltraAC10",1);            

        AddMechInstanceSalvage("Daishi");
        AddMechInstanceSalvage("Sunder");
        
        AddWeaponToMarket("Thunderbolt",2);
        AddWeaponToMarket("ClanUltraAC20",1);
      //
        
        trans won;
    endif;

// Betty speaks when reaching Nav Point

    if (not alphareached) then
	    if (isnavrevealed(ena_nav_alpha)) then
		    if (iswithin(playermech,ena_nav_alpha,checkrange)) then
			    playvoiceover("VO\generic\navalpha",talker_Bet);
			    alphareached = true;
                revealnavpoint(ena_nav_beta);
                setnavpoint(ena_nav_beta);
		    endif;
	    endif;
    endif;

// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\EriDestroyedKnight",talker_BAD);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
		cinema_cut = cinema_cut + 1;
        trans lost;
	endif;

				
endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;
	code

    	if (cinemaskip) then
        	KillVoiceOvers;
    		cinema_cut = 0;
    		KillTimer(gti_timer_21);
    		SetGlobalTrigger(0,true);
    		playerai(epl_player0,false);
    		CinemaEnd;
            SetCompositingEnabled(True);
            
    		trans sit;
        endif;
        
        switch (cinema_cut)
			
			case 0:
                SetCompositingEnabled(False);
                SetActiveCamera(eca_CameraShip);

                PlayVoiceOverOnce("VOX\OP4\MI4\Intro",talker_ERI);
                PlayLancemateSound("VOX\OP4\MI4\Intro",1);
                PlayVoiceOverOnce("VOX\OP4\MI4\Intro2",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI4\Intro3",talker_ERI);

        		playerai(epl_player0,true);
                CinemaStart;
                SetAlignment(GroupObjectID(10),neutral);
                
                FadeToBlack(0.0);
                starttimer(1);
                resettimer(1);
                
                Targetfollowobject(ena_helloian);
                Camerafollowobject(ena_helloian);
                Targetoffset(0.0,10.0,-400.0,0.0,false);
                Targetoffset(0.0,150.0,1300.0,30.0,false);
                Cameraoffset(0.0,10.0,-600.0,0.0,false);
                Cameraoffset(0.0,150.0,1100.0,30.0,false);
                //Targetoffset(0.0,20.0,0.0,0.0,false);
                //Targetoffset(0.0,20.0,0.0,30.0,false);
                //Cameraoffset(300.0,15.0,675.0,0.0,false);
                //Cameraoffset(0.0,30.0,100.0,30.0,false);
                FadeFromBlack(3.0);
                
                cinema_cut = cinema_cut + 1;
			endcase;
			
            case 1:
                if (timegreater(1,31)) then
                    KillVoiceOvers;
                    cinema_cut = 0;
                    KillTimer(gti_timer_21);
                    SetGlobalTrigger(0,true);
                    playerai(epl_player0,false);
                    CinemaEnd;
                    SetCompositingEnabled(True);
                    
                    trans sit;
                endif;
			endcase;
			
		endswitch;
	
endstate;

state won;
    code

        if (cinemaskip) then
            cinema_cut = 20;
            resettimer (gti_Timer_21);
            killtimer(gti_timer_21);
            endmission(true,1);	
        endif;
    
    	switch (cinema_cut)
    			
            case 0:
                SetCompositingEnabled(False);
                CinemaStart;
                
                endmission(true,17);
        		playerai(epl_player0,true);
                StartMusic("Music\Victory",3.0,3.5,50);

                /* lines stolen for Volcano04
                if (REBEL_FRIEND) then
                    PlayVoiceOverOnce("VOX\OP4\MI4\Victory",talker_BAD);
                    PlayVoiceOverOnce("VOX\OP4\MI4\Victory2",talker_ERI);
                endif; */
                                
                SetActiveCamera (eca_CameraShip);
                
                FadeToBlack(0.0);				

                StartTimer(gti_Timer_21);
                ResetTimer(gti_Timer_21);
                cinema_cut = cinema_cut + 1;
            
            endcase;
            
            case 1:
    			if (TimeGreater(gti_Timer_21,2)) then
                    //ResetTimer(gti_Timer_21);
                    Targetfollowobject(playermech);
                    camerafollowobject(ena_outronav);
                    targetoffset(0.0,0.0,0.0,0.0,false);
                    Cameraoffset(300.0,45.0,0.0,0.0,true);
                    Cameraoffset(-285.0,45.0,-80.0,21.0,true);
                    FadeFromBlack(2.0);
                    cinema_cut = cinema_cut + 1;
        		endif;
            endcase;
        
            case 2:
                if (TimeGreater(gti_Timer_21,4)) then
                    PlayVoiceOverOnce("VOX\OP4\MI4\Victory",talker_ERI);
                endif;
            endcase;
            
        endswitch;

endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;


endfsm.
//******************************************************************

