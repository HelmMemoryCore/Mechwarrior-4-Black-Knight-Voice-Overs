
//*********************************************************************************

fsm ruins01_StagingGround : integer;		

//------------------------------------------------------------------
//
//     Constant Definitions
//
//------------------------------------------------------------------
     const

		#include_ <content\ABLScripts\mwconst.abi>
//------------------------------------------------------------------
//
//     Type Definitions
//
//------------------------------------------------------------------
     type

		#include_ <content\ABLScripts\mwtype.abi>

//------------------------------------------------------------------
//
//     Variable Declarations
//
//------------------------------------------------------------------
      var
			
		eternal integer     playermech;
		
        // Standard Nav and Objective Variables
        static boolean	    alphareached;       // Nav Alpha reached
        static boolean	    betareached;        // Nav Beta reached
        static boolean	    gammareached;       // Nav Gamma reached
        static boolean	    deltareached;       // Nav Delta reached
		                                        //
        static boolean	    alpharevealed;      // Nav Alpha is shown
        static boolean	    betarevealed;       // Nav Beta is shown
        static boolean	    gammarevealed;      // Nav Gamma is shown
        static boolean	    deltarevealed;      // Nav Delta is shown
                                                //
        static boolean      obj1;               // This indicates whether Obj 1 was completed
        eternal boolean     obj2;               // This indicates whether Obj 2 was completed
        eternal boolean     obj3;               // This indicates whether Obj 3 was completed
        eternal boolean     obj4;               // This indicates whether Obj 4 was completed
        eternal boolean     obj5;               // This indicates whether Obj 5 was completed
        eternal boolean     obj6;               // This indicates whether Obj 6 was completed
                                                //
        static boolean      obj1revealed;       // This indicates that Obj 1 has been revealed
        static boolean      obj2revealed;       // This indicates that Obj 2 has been revealed
        static boolean      obj3revealed;       // This indicates that Obj 3 has been revealed
        static boolean      obj4revealed;       // This indicates that Obj 4 has been revealed
        //    

		static integer	    cinema_cut;
        static integer      wimpy;              // For the easy setting
        static locpoint     alphaleash;         // This is the locpoint of Nav Alpha
        static locpoint     betaleash;          // This is the locpoint of Nav Alpha
        static integer      checkrange;         // How far from the navpoint for Betty
        eternal boolean     counterstrike;      // The enemy counterstrike at Nav Delta begins
        eternal boolean     ECMoff;             // This determines when the ECM is off
        eternal boolean     REBEL_FRIEND;       // Has the player helped out the rebels in the past
        static integer      tempcounter;        // This is to determine when to show Nav Gamma
        static locpoint     tempnav;            // Used for teleporting
        eternal boolean     betago;             // The beta vehicles should mobilize
        
//------------------------------------------------------------------
//
//     Init Function    (automatically run first time through)
//
//------------------------------------------------------------------

function init;
	var
		integer i;

	code

        playermech  = getplayervehicle(epl_player0);	
        GetLocation(ena_Nav_Alpha,alphaleash);
        GetLocation(ena_Nav_Beta,betaleash);
    
		GroupAddObject(GroupObjectId(1),playermech);
        GroupAddObject(GroupObjectId(44),playermech);
        GroupAddObject(GroupObjectId(45),playermech);
        GroupAddObject(GroupObjectId(61),playermech);

        if (GetLancemate(1)<>NO_UNIT) then
            GroupAddObject(GroupObjectId(1),GetLancemate(1));
            GroupAddObject(GroupObjectId(2),GetLancemate(1));
            GroupAddObject(GroupObjectId(44),GetLancemate(1));
            GroupAddObject(GroupObjectId(45),GetLancemate(1));
            GroupAddObject(GroupObjectId(61),GetLancemate(1));
        endif;
        
        if (GetLancemate(2)<>NO_UNIT) then
            GroupAddObject(GroupObjectId(1),GetLancemate(2));
            GroupAddObject(GroupObjectId(2),GetLancemate(2));
            GroupAddObject(GroupObjectId(44),GetLancemate(2));
            GroupAddObject(GroupObjectId(45),GetLancemate(2));
            GroupAddObject(GroupObjectId(61),GetLancemate(2));
        endif;
        
        if (GetLancemate(3)<>NO_UNIT) then
            GroupAddObject(GroupObjectId(1),GetLancemate(3));
            GroupAddObject(GroupObjectId(2),GetLancemate(3));
            GroupAddObject(GroupObjectId(44),GetLancemate(3));
            GroupAddObject(GroupObjectId(45),GetLancemate(3));
            GroupAddObject(GroupObjectId(61),GetLancemate(3));
        endif;

        // Rebel Skirmish at Nav Alpha
        GroupAddObject(GroupObjectId(20),eve_Rebel_Hellspawn);
        GroupAddObject(GroupObjectId(20),eve_Rebel_Ryoken);
        GroupAddObject(GroupObjectId(20),eve_Rebel_Uziel);
        SetCombatLeash(GroupObjectId(20),alphaleash,250);
        SetGroupAI(groupobjectid(20),GROUPAI_MOODSQUAD);
        
        // Rebel Skirmish at Nav Beta
        GroupAddObject(GroupObjectId(21),eve_brebel_0001);
        GroupAddObject(GroupObjectId(21),eve_brebel_0002);
        GroupAddObject(GroupObjectId(21),eve_brebel_0003);
        GroupAddObject(GroupObjectId(21),eve_brebel_0004);
        GroupAddObject(GroupObjectId(21),eve_brebel_0005);
        SetCombatLeash(GroupObjectId(21),betaleash,250);
        SetGroupAI(groupobjectid(21),GROUPAI_MOODSQUAD);
        
        // Enemy ECM buildings at Nav Beta
        GroupAddObject(GroupObjectId(30),ebu_eccm_generator);
        // GroupAddObject(GroupObjectId(30),ebu_eccm_transmitter);
        // GroupAddObject(GroupObjectId(30),ebu_backup_eccm_transmitter);
        
        // Dressari Skirmish at Nav Alpha
        GroupAddObject(GroupObjectId(40),eve_adressari_01);
        GroupAddObject(GroupObjectId(40),eve_adressari_02);
        GroupAddObject(GroupObjectId(40),eve_adressari_03);
        GroupAddObject(GroupObjectId(40),eve_adressari_04);
        SetCombatLeash(GroupObjectId(40),alphaleash,250);
        SetGroupAI(groupobjectid(40),GROUPAI_MOODSQUAD);

        // Dressari Skirmish at Nav Beta
        GroupAddObject(GroupObjectId(41),eve_bdressari_01);
        GroupAddObject(GroupObjectId(41),eve_bdressari_02);
        GroupAddObject(GroupObjectId(41),eve_bvehicle_0001);
        GroupAddObject(GroupObjectId(41),eve_bvehicle_0002);
        GroupAddObject(GroupObjectId(41),eve_bvehicle_0003);
        GroupAddObject(GroupObjectId(41),eve_bvehicle_0004);
        GroupAddObject(GroupObjectId(41),eve_bvehicle_0005);
        GroupAddObject(GroupObjectId(41),eve_bmissile_0001);
        GroupAddObject(GroupObjectId(41),eve_bmissile_0002);
        GroupAddObject(GroupObjectId(41),eve_bmissile_0003);
        GroupAddObject(GroupObjectId(41),eve_bmissile_0004);
        SetCombatLeash(GroupObjectId(41),betaleash,300);
        SetGroupAI(groupobjectid(41),GROUPAI_MOODSQUAD);

        // Dressari Skirmish at Nav Gamma
        // GroupAddObject(GroupObjectId(42),eve_gdressari_01);
        // Madcat_mkii, gdressari_01, Skin - 35, Enemy, MechAI, Skirmish, Memory Integer 2, x -1455 / y 49 / z -618, Yaw 170
        GroupAddObject(GroupObjectId(42),eve_gdressari_02);
        GroupAddObject(GroupObjectId(42),eve_gdressari_03);
        GroupAddObject(GroupObjectId(42),etu_lrm_turret);
        GroupAddObject(GroupObjectId(42),etu_lrm_turret_0001);
        SetGroupAI(groupobjectid(42),GROUPAI_MOODSQUAD);

        // Dressari Skirmish at Nav Delta (counterstrike)
        GroupAddObject(GroupObjectId(43),eve_ddressari_01);
        GroupAddObject(GroupObjectId(43),eve_ddressari_02);
        //GroupAddObject(GroupObjectId(43),eve_ddressari_03);
        GroupAddObject(GroupObjectId(43),eve_ddressari_04);
        GroupAddObject(GroupObjectId(43),eve_ddressari_05);
        SetGroupAI(groupobjectid(43),GROUPAI_MOODSQUAD);
    	
        // Marauder Skirmish at Nav Alpha
        GroupAddObject(GroupObjectId(60),eve_amarauder_01);
        GroupAddObject(GroupObjectId(60),eve_amarauder_02);
        GroupAddObject(GroupObjectId(60),eve_amarauder_03);
        GroupAddObject(GroupObjectId(60),eve_amarauder_04);
        GroupAddObject(GroupObjectId(60),eve_amarauder_05);
        GroupAddObject(GroupObjectId(60),eve_amarauder_06);
        SetCombatLeash(GroupObjectId(60),alphaleash,300);
        SetGroupAI(groupobjectid(60),GROUPAI_MOODSQUAD);        
        
        // Valid Targets for Rebels at Alpha (PC unfriendly)
        GroupAddObject(GroupObjectId(22),eve_adressari_01);
        GroupAddObject(GroupObjectId(22),eve_adressari_02);
        GroupAddObject(GroupObjectId(22),eve_adressari_03);
        GroupAddObject(GroupObjectId(22),eve_adressari_04);
        GroupAddObject(GroupObjectId(22),eve_amarauder_01);
        GroupAddObject(GroupObjectId(22),eve_amarauder_02);
        GroupAddObject(GroupObjectId(22),eve_amarauder_03);
        GroupAddObject(GroupObjectId(22),eve_amarauder_04);
        GroupAddObject(GroupObjectId(22),eve_amarauder_05);
        GroupAddObject(GroupObjectId(22),eve_amarauder_06);

        // Valid Targets for Rebels at Alpha (PC unfriendly)
        GroupAddObject(GroupObjectId(23),eve_adressari_01);
        GroupAddObject(GroupObjectId(23),eve_adressari_02);
        GroupAddObject(GroupObjectId(23),eve_adressari_03);
        GroupAddObject(GroupObjectId(23),eve_adressari_04);
        GroupAddObject(GroupObjectId(23),eve_amarauder_01);
        GroupAddObject(GroupObjectId(23),eve_amarauder_02);
        GroupAddObject(GroupObjectId(23),eve_amarauder_03);
        GroupAddObject(GroupObjectId(23),eve_amarauder_04);
        GroupAddObject(GroupObjectId(23),eve_amarauder_05);
        GroupAddObject(GroupObjectId(23),eve_amarauder_06);
        
        // Valid Targets for Marauders at Alpha
        GroupAddObject(GroupObjectId(61),eve_Rebel_Hellspawn);
        GroupAddObject(GroupObjectId(61),eve_Rebel_Ryoken);
        GroupAddObject(GroupObjectId(61),eve_Rebel_Uziel);
        GroupAddObject(GroupObjectId(61),eve_adressari_01);
        GroupAddObject(GroupObjectId(61),eve_adressari_02);
        GroupAddObject(GroupObjectId(61),eve_adressari_03);
        GroupAddObject(GroupObjectId(61),eve_adressari_04);

        // Valid Targets for Dresari at Alpha
        GroupAddObject(GroupObjectId(44),eve_amarauder_01);
        GroupAddObject(GroupObjectId(44),eve_amarauder_02);
        GroupAddObject(GroupObjectId(44),eve_amarauder_03);
        GroupAddObject(GroupObjectId(44),eve_amarauder_04);
        GroupAddObject(GroupObjectId(44),eve_amarauder_05);
        GroupAddObject(GroupObjectId(44),eve_amarauder_06);
        GroupAddObject(GroupObjectId(44),eve_Rebel_Hellspawn);
        GroupAddObject(GroupObjectId(44),eve_Rebel_Ryoken);
        GroupAddObject(GroupObjectId(44),eve_Rebel_Uziel);
        
        // Valid Targets for Lancemates at Alpha (rebel friendly)
        GroupAddObject(GroupObjectId(3),eve_adressari_01);
        GroupAddObject(GroupObjectId(3),eve_adressari_02);
        GroupAddObject(GroupObjectId(3),eve_adressari_03);
        GroupAddObject(GroupObjectId(3),eve_adressari_04);
        GroupAddObject(GroupObjectId(3),eve_amarauder_01);
        GroupAddObject(GroupObjectId(3),eve_amarauder_02);
        GroupAddObject(GroupObjectId(3),eve_amarauder_03);
        GroupAddObject(GroupObjectId(3),eve_amarauder_04);
        GroupAddObject(GroupObjectId(3),eve_amarauder_05);
        GroupAddObject(GroupObjectId(3),eve_amarauder_06);
        
        // Valid Targets for Lancemates at Alpha (rebel unfriendly)
        GroupAddObject(GroupObjectId(4),eve_adressari_01);
        GroupAddObject(GroupObjectId(4),eve_adressari_02);
        GroupAddObject(GroupObjectId(4),eve_adressari_03);
        GroupAddObject(GroupObjectId(4),eve_adressari_04);
        GroupAddObject(GroupObjectId(4),eve_amarauder_01);
        GroupAddObject(GroupObjectId(4),eve_amarauder_02);
        GroupAddObject(GroupObjectId(4),eve_amarauder_03);
        GroupAddObject(GroupObjectId(4),eve_amarauder_04);
        GroupAddObject(GroupObjectId(4),eve_amarauder_05);
        GroupAddObject(GroupObjectId(4),eve_amarauder_06);
        GroupAddObject(GroupObjectId(4),eve_Rebel_Hellspawn);
        GroupAddObject(GroupObjectId(4),eve_Rebel_Ryoken);
        GroupAddObject(GroupObjectId(4),eve_Rebel_Uziel);

        // Valid Targets for Rebels at Beta
        GroupAddObject(GroupObjectId(24),eve_bdressari_01);
        GroupAddObject(GroupObjectId(24),eve_bdressari_02);
        GroupAddObject(GroupObjectId(24),eve_bvehicle_0001);
        GroupAddObject(GroupObjectId(24),eve_bvehicle_0002);
        GroupAddObject(GroupObjectId(24),eve_bvehicle_0003);
        GroupAddObject(GroupObjectId(24),eve_bvehicle_0004);
        GroupAddObject(GroupObjectId(24),eve_bvehicle_0005);
        GroupAddObject(GroupObjectId(24),eve_bmissile_0001);
        GroupAddObject(GroupObjectId(24),eve_bmissile_0002);
        GroupAddObject(GroupObjectId(24),eve_bmissile_0003);
        GroupAddObject(GroupObjectId(24),eve_bmissile_0004);

        // Valid Targets for Dresari at Beta
        GroupAddObject(GroupObjectId(45),eve_brebel_0001);
        GroupAddObject(GroupObjectId(45),eve_brebel_0002);
        GroupAddObject(GroupObjectId(45),eve_brebel_0003);
        GroupAddObject(GroupObjectId(45),eve_brebel_0004);
        GroupAddObject(GroupObjectId(45),eve_brebel_0005);
        
        // Valid Targets for Lancemates at Beta (not rebel friend)
        GroupAddObject(GroupObjectId(5),eve_bdressari_01);
        GroupAddObject(GroupObjectId(5),eve_bdressari_02);
        GroupAddObject(GroupObjectId(5),eve_bvehicle_0001);
        GroupAddObject(GroupObjectId(5),eve_bvehicle_0002);
        GroupAddObject(GroupObjectId(5),eve_bvehicle_0003);
        GroupAddObject(GroupObjectId(5),eve_bvehicle_0004);
        GroupAddObject(GroupObjectId(5),eve_bvehicle_0005);
        GroupAddObject(GroupObjectId(5),eve_bmissile_0001);
        GroupAddObject(GroupObjectId(5),eve_bmissile_0002);
        GroupAddObject(GroupObjectId(5),eve_bmissile_0003);
        GroupAddObject(GroupObjectId(5),eve_bmissile_0004);
        GroupAddObject(GroupObjectId(5),eve_brebel_0001);
        GroupAddObject(GroupObjectId(5),eve_brebel_0002);
        GroupAddObject(GroupObjectId(5),eve_brebel_0003);
        GroupAddObject(GroupObjectId(5),eve_brebel_0004);
        GroupAddObject(GroupObjectId(5),eve_brebel_0005);
        
        // Valid Targets for Lancemates at Beta (rebel friend)
        GroupAddObject(GroupObjectId(6),eve_bdressari_01);
        GroupAddObject(GroupObjectId(6),eve_bdressari_02);
        GroupAddObject(GroupObjectId(6),eve_bvehicle_0001);
        GroupAddObject(GroupObjectId(6),eve_bvehicle_0002);
        GroupAddObject(GroupObjectId(6),eve_bvehicle_0003);
        GroupAddObject(GroupObjectId(6),eve_bvehicle_0004);
        GroupAddObject(GroupObjectId(6),eve_bvehicle_0005);
        GroupAddObject(GroupObjectId(6),eve_bmissile_0001);
        GroupAddObject(GroupObjectId(6),eve_bmissile_0002);
        GroupAddObject(GroupObjectId(6),eve_bmissile_0003);
        GroupAddObject(GroupObjectId(6),eve_bmissile_0004);
        
		cinema_cut = 0; 						
		
endfunction;

//------------------------------------------------------------------
//
//     Main Code
//
//------------------------------------------------------------------
state startState;

	code	
	    //playsound(eso_start);
		setactivationdistance(1200);
		// settargetdesirability(playermech,-1);

        setradarnav(ena_Nav_Alpha,1);
        setradarnav(ena_Nav_Beta,2);
        setradarnav(ena_Nav_Gamma,3);

        revealnavpoint(ena_Nav_Alpha);
        setnavpoint(ena_Nav_Alpha);

        revealobjective(eob_objective1);
        revealobjective(eob_objective2);
        revealobjective(eob_objective5);
        
        settargetdesirability(GroupObjectID(21),-1);

        REBEL_FRIEND = GetEventFlag("REBEL_FRIEND");
        
        if (REBEL_FRIEND) then
            SetAlignment(GroupObjectID(20),friendly);
            SetAlignment(GroupObjectID(21),friendly);
        endif;
        
        checkrange=150;
        CinemaStart;
        StartMusic("Music\Wednesday",3.0,3.5,60);

        Wimpy = GetDifficulty;

        // In easy mode take out the two powered down base 'Mechs
        If (wimpy == EASY_DIFFICULTY) then
            //Destroy(eve_ddressari_03);
            Destroy(eve_ddressari_01);
            Destroy(eve_adressari_03);
            Destroy(eve_adressari_04);
            if (not REBEL_FRIEND) then
                Destroy(eve_Rebel_Hellspawn);
            endif;
        endif;
        
	    trans opening;
        //trans sit;

endstate;

state deadState;
	code
endstate;
	
//------------------------------------------------------------------
//
//     Sit state : description
//
//------------------------------------------------------------------
state sit;

	code

    if (not ismusicplaying) then
    	StartMusic("Music\urbanambloop",0.0,3.0,40);
    endif;

// Audio Triggers

    if (not GetGlobalTrigger(3)) then        // Eric warning the crew he can't give them targets
        if (timegreater(1,7)) then
            SetGlobalTrigger(3,true);
            PlayVoiceOverOnce("VOX\OP4\MI1\Designate",talker_ERI);
        endif;
    endif;
    
    if (not GetGlobalTrigger(4)) then        // No friend of the rebels
        if (REBEL_FRIEND) then
            if (iswithin(playermech,ena_nav_alpha,400)) then
                SetGlobalTrigger(4,true);
                PlayVoiceOverOnce("VOX\OP4\MI1\Friend",talker_BAD);
            endif;
        else
            if (iswithin(playermech,ena_nav_alpha,400)) then
                SetGlobalTrigger(4,true);
                PlayVoiceOverOnce("VOX\OP4\MI1\NoFriend",talker_BAD);
            endif;
        endif;
    endif;

    if (not GetGlobalTrigger(5)) then        // Radcliffe wanting his radar back
        if (timegreater(1,20)) then
            SetGlobalTrigger(5,true);
            PlayLancemateSound("VOX\OP4\MI1\Comfortable",1);
        endif;
    endif;

    if (not GetGlobalTrigger(6)) then        // ECM spotted
        if (isdead(ebu_eccm_generator)) then
            SetGlobalTrigger(6,true);
        else
            if (iswithin(playermech,ebu_eccm_generator,350)) then
                SetGlobalTrigger(6,true);
                PlayVoiceOverOnce("VOX\OP4\MI1\Spotted",talker_ERI);
            endif;
        endif;
    endif;
    
// Triggers - This controls when vehicles start attacking each other

    if (not ECMoff) then
        if (not obj2) then
            SetTarget(GroupObjectID(40),GroupObjectID(44));
            SetTarget(GroupObjectID(60),GroupObjectID(61));
            if (REBEL_FRIEND) then
                SetTarget(GroupObjectID(20),GroupObjectID(23));
                SetTarget(GroupObjectID(2),GroupObjectID(3));
            else
                SetTarget(GroupObjectID(20),GroupObjectID(22));
                SetTarget(GroupObjectID(2),GroupObjectID(4));
            endif;
        endif;
    endif;

    if (not ECMoff) then
        if (obj2) then
            SetTarget(GroupObjectID(41),GroupObjectID(45));
            SetTarget(GroupObjectID(21),GroupObjectID(24));
            if (REBEL_FRIEND) then
                SetTarget(GroupObjectID(2),GroupObjectID(6));
            else
                SetTarget(GroupObjectID(2),GroupObjectID(5));
            endif;
        endif;
    endif;
    
    if (not GetGlobalTrigger(11)) then
        if (REBEL_FRIEND) then
            if (obj2) then
                if (not iswithin(playermech,GroupObjectID(20),900)) then
                    Shutdown(GroupObjectID(20));
                    TeleporttoHell(GroupObjectID(20));
                    Destroy(GroupObjectID(20));
                    SetGlobalTrigger(11,true);
                endif;
            endif;
        endif;
    endif;
    
    if (not betago) then
        if (iswithin(playermech,ena_nav_beta,800)) then
            betago = true;
        endif;
    endif;
    		
//Reveal the objectives

// Objective 1 - Destroy the Communications Facility

    if (not obj1) then
        if (gammareached) then
            successobjective(eob_objective1);
            PlayVoiceOverOnce("VOX\OP4\MI1\Gamma",talker_ERI);
            obj1=true;
        endif;
    endif;

// Objective 2 - Investigate Nav Alpha

    if (not obj2) then
        if (REBEL_FRIEND) then
            if (GroupAllDead(GroupObjectID(40))) then
                obj2=true;
                successobjective(eob_objective2);
                revealobjective(eob_objective3);
                revealnavpoint(ena_Nav_Beta);
                setnavpoint(ena_Nav_Beta);
                PlayVoiceOverOnce("VOX\OP4\MI1\Alpha",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI1\NoSign",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI1\Generator",talker_BAD);
            endif;
       else
            if (GroupAllDead(GroupObjectID(40))) then
                if (GroupAllDead(GroupObjectID(20))) then
                    obj2=true;
                    successobjective(eob_objective2);
                    revealobjective(eob_objective3);
                    revealnavpoint(ena_Nav_Beta);
                    setnavpoint(ena_Nav_Beta);
                    PlayVoiceOverOnce("VOX\OP4\MI1\Alpha",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI1\NoSign",talker_ERI);
                    PlayVoiceOverOnce("VOX\OP4\MI1\Generator",talker_BAD);                
                endif;
            endif;
        endif;
    endif;

// Objective 3 - Investigate Nav Beta

    if (not obj3) then
        if (REBEL_FRIEND) then
            if (GroupAllDead(GroupObjectID(6))) then
                obj3=true;
                successobjective(eob_objective3);
                revealobjective(eob_objective4);
                revealnavpoint(ena_Nav_Gamma);
                setnavpoint(ena_Nav_Gamma);
                PlayVoiceOverOnce("VOX\OP4\MI1\Beta",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI1\Staging",talker_BAD);
            endif;
        else
            if (GroupAllDead(GroupObjectID(5))) then
                obj3=true;
                successobjective(eob_objective3);
                revealobjective(eob_objective4);
                revealnavpoint(ena_Nav_Gamma);
                setnavpoint(ena_Nav_Gamma);
                PlayVoiceOverOnce("VOX\OP4\MI1\Beta",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI1\Staging",talker_BAD);
            endif;
        endif;        
    endif;

// Objective 4 - Investigate Nav Gamma

    if (not obj4) then
        if (GroupAllDead(GroupObjectID(42))) then
            obj4=true;
            successobjective(eob_objective4);
            PlayVoiceOverOnce("VOX\OP4\MI1\Eliminated",talker_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI1\Gamma",talker_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI1\Activity",talker_ERI);
        endif;
    endif;

    if (not GetGlobalTrigger(2)) then   // Once you destroy Gamma and investigate it then movie happens
        if (obj4) then
            if (obj1) then
                starttimer(9);      // This timer is for when the counter strike happens
                SetGlobalTrigger(2,true);
            endif;
        endif;
    endif;

    if (not GetGlobalTrigger(1)) then
        if (timegreater(9,5)) then
            SetGlobalTrigger(1,true);
            counterstrike=true;
            trans fakewon;
        endif;
    endif;
    
    if (not GetGlobalTrigger(10)) then
        if (counterstrike) then
            if (iswithin(playermech,GroupObjectID(43),600)) then
                SetGlobalTrigger(10,true);
            endif;
        endif;
    endif;

/* Objective 5 - Eliminate the source of ECM Jamming
        Used to be Investigate Nav Delta */

    if (not obj5) then
        if (GroupAllDead(GroupObjectID(30))) then
            obj5=true;
            successobjective(eob_objective5);
            SetSensorVisibility(GroupObjectID(20),true);   // Now you can see everyone
            SetSensorVisibility(GroupObjectID(21),true);
            SetSensorVisibility(GroupObjectID(40),true);
            SetSensorVisibility(GroupObjectID(41),true);
            SetSensorVisibility(GroupObjectID(60),true);
            PlayVoiceOverOnce("VOX\OP4\MI1\Back",talker_ERI);
            PlayLancemateSound("VOX\OP4\MI1\Back",1);
            ECMoff = true;
        endif;
    endif;
    
// Objective 6 - Defeat the Dressari counterstrike    
    
    if (obj5) then
        if (not obj6) then
            if (GroupAllDead(GroupObjectID(43))) then
                successobjective(eob_objective6);
                starttimer(19);
                obj6 = true;
            endif;
        endif;
    endif;

    if (timegreater(19,5)) then     // Delay the mission end

      // Salvage Added by Ferret 7/28/01         
        AddWeaponSalvage("ERPPC",1);
        AddWeaponSalvage("ERMediumPulseLaser",2);            
        AddWeaponSalvage("ERLargeLaser",2);
        AddWeaponSalvage("ClanLRM15",2);
        AddWeaponSalvage("UltraAC20",1);

        AddMechInstanceSalvage("Atlas");
        AddMechInstanceSalvage("Awesome");
        
        AddWeaponToMarket("AC20",1);
        AddWeaponToMarket("UltraAC10",1);
        
        AddMechToMarket("Daishi");
      //
        
        trans won;
    endif;

// Betty speaks when reaching Nav Point

    if (not alphareached) then
	    if (isnavrevealed(ena_nav_alpha)) then
		    if (iswithin(playermech,ena_nav_alpha,checkrange)) then
			    playvoiceover("VO\generic\navalpha",talker_Bet);
			    alphareached = true;
		    endif;
	    endif;
    endif;

    if (not betareached) then
    	if (isnavrevealed(ena_nav_beta)) then
    		if (iswithin(playermech,ena_nav_beta,checkrange)) then
    			playvoiceover("VO\generic\navbeta",talker_Bet);
    			betareached = true;
    		endif;
    	endif;
    endif;
    
    if (not gammareached) then
    	if (isnavrevealed(ena_nav_gamma)) then
    		if (iswithin(playermech,ena_nav_gamma,checkrange)) then
    			playvoiceover("VO\generic\navgamma",talker_Bet);
                PlayVoiceOverOnce("VOX\OP4\MI1\Gamma",talker_ERI);
    			gammareached = true;
    		endif;
    	endif;
    endif;
    
// Fail the mission when the player dies
	if (isdead(playermech)) then
		KillVoiceOvers;
		PlayVoiceOverOnce("VOX\Generic\EriDestroyedKnight3",talker_BAD);
		StartMusic("Music\Defeat",3.0,3.5,70);
		endmission(false,14);
		cinema_cut = cinema_cut + 1;
        trans lost;
	endif;

endstate;

//-----------------------------------------
//HERE THERE BE CINEMAS
//-----------------------------------------

state opening;

	code

	if (cinemaskip) then
    	KillVoiceOvers;
		cinema_cut = 0;
		KillTimer(gti_timer_21);
		SetGlobalTrigger(0,true);
		playerai(epl_player0,false);
		CinemaEnd;
        SetCompositingEnabled(True);
        resettimer(1);
        
        startup(GroupObjectID(2));
        
        // The ECM is currently on
        // SetSensorVisibility(GroupObjectID(2),false);
        SetSensorVisibility(GroupObjectID(20),false);
        SetSensorVisibility(GroupObjectID(21),false);
        SetSensorVisibility(GroupObjectID(40),false);
        SetSensorVisibility(GroupObjectID(41),false);
        SetSensorVisibility(GroupObjectID(60),false);

		trans sit;
    endif;
        
        switch (cinema_cut)
			
			case 0:
                SetActiveCamera(eca_CameraShip);
                SetCompositingEnabled(False);

                //PlayVoiceOverOnce("VOX\OP4\MI1\Intro",talker_BAD);
                PlayVoiceOverOnce("VOX\OP4\MI1\Intro",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI1\Intro2",talker_BAD);
                
                FadeToBlack(0.0);
                starttimer(1);
                resettimer(1);
                
                shutdown(GroupObjectID(2));

                Targetfollowobject(eve_adressari_01);
                Camerafollowobject(ena_intromovie);
                Targetoffset(0.0,4.0,0.0,0.0,false);
                Cameraoffset(30.0,70.0,-20.0,0.0,false);
                Cameraoffset(100.0,60.0,-110.0,14.0,false);
                FadeFromBlack(7.0);
                
                cinema_cut = cinema_cut + 1;
			endcase;
			
			case 1:
                if (timegreater(1,22)) then
                    cinema_cut = cinema_cut + 1;
                    startup(GroupObjectID(2));
                endif;
			endcase;

			case 2:
                if (timegreater(1,24)) then
                    cinema_cut = cinema_cut + 1;
                    resettimer(1);
                    KillTimer(gti_timer_21);
                endif;
			endcase;
			
            case 3:
                KillVoiceOvers;
                cinema_cut = 0;
                KillTimer(gti_timer_21);
                SetGlobalTrigger(0,true);
                playerai(epl_player0,false);
                CinemaEnd;
                SetCompositingEnabled(True);
                
                // The ECM is currently on
                // SetSensorVisibility(GroupObjectID(2),false);
                SetSensorVisibility(GroupObjectID(20),false);
                SetSensorVisibility(GroupObjectID(21),false);
                SetSensorVisibility(GroupObjectID(40),false);
                SetSensorVisibility(GroupObjectID(41),false);
                SetSensorVisibility(GroupObjectID(60),false);
                
                trans sit;
			endcase;
			
		endswitch;
	
endstate;


state won;
    code
    
        if (cinemaskip) then
            cinema_cut = 20;
            resettimer (gti_Timer_21);
            killtimer(gti_timer_21);
            endmission(true,1);	
            trans hang;
        endif;
    
        switch cinema_cut
    
        case 0:	
            SetCompositingEnabled(False);
            CinemaStart;
            setactivecamera (eca_cameraship);
    		playerai(epl_player0,true);
            //Fadetoblack(7.0);
            Targetfollowobject(playermech);
            Camerafollowobject(playermech);
            Targetoffset(0.0,4.0,0.0,0.0,false);
            Cameraoffset(-30.0,125.0,75.0,0.0,false);
            Cameraoffset(60.0,20.0,25.0,15.0,false);
            PlayVoiceOverOnce("VOX\OP4\MI1\Victory",talker_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI1\Victory",talker_ERI);
            StartMusic("Music\Victory",3.0,3.5,50);
            endmission(true,15);
            Cinema_cut=Cinema_cut+1;
        endcase;
        
        case 1:
        endcase;
			
		endswitch;
	
endstate;

state fakewon;
    code
    
        if (cinemaskip) then

        	KillVoiceOvers;
    		cinema_cut = 0;
    		KillTimer(gti_timer_21);
    		SetGlobalTrigger(0,true);
    		playerai(epl_player0,false);
    		CinemaEnd;
            Startup(GroupObjectID(43));
            revealobjective(eob_objective6);

            PlayVoiceOverOnce("VOX\OP4\MI1\Contact",talker_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI1\Defense",talker_ERI);
            PlayVoiceOverOnce("VOX\OP4\MI1\Defense",talker_BAD);
            PlayVoiceOverOnce("VOX\OP4\MI1\Defense2",talker_ERI);
            StartMusic("Music\DFA",3.0,3.5,70);
            
            trans sit;
        endif;
    
        switch cinema_cut
    
        case 0:	
            SetCompositingEnabled(False);
            CinemaStart;
    		playerai(epl_player0,true);
            setactivecamera (eca_cameraship);
            //Fadetoblack(7.0);
            Targetfollowobject(ena_nav_gamma);
            Camerafollowobject(ena_nav_gamma);
            GetLocation(ena_nav_gamma,tempnav);
            teleport(playermech,tempnav);
            Shutdown(GroupObjectID(43));
            Targetoffset(0.0,0.0,0.0,0.0,false);
            Targetoffset(20.0,0.0,5.0,15.0,false);
            Cameraoffset(-300.0,45.0,-80.0,0.0,false);
            Cameraoffset(150.0,30.0,-80.0,15.0,false);
            PlayVoiceOverOnce("VOX\OP4\MI1\Gamma",talker_BAD);
            
            StartMusic("Music\Victory",3.0,3.5,50);

            starttimer (gti_Timer_21);
            resettimer (gti_Timer_21);
            
            Cinema_cut=Cinema_cut+1;
        endcase;
        
        case 1:
            if (timegreater(21,12)) then
                PlayVoiceOverOnce("VOX\OP4\MI1\Contact",talker_BAD);
            endif;
            
            if (timegreater(21,16)) then
                PlayVoiceOverOnce("VOX\OP4\MI1\Defense",talker_ERI);
                PlayVoiceOverOnce("VOX\OP4\MI1\Defense",talker_BAD);
                PlayVoiceOverOnce("VOX\OP4\MI1\Defense2",talker_ERI);

        		cinema_cut = 0;
        		KillTimer(gti_timer_21);
        		SetGlobalTrigger(0,true);
        		playerai(epl_player0,false);
        		CinemaEnd;
                StartMusic("Music\DFA",3.0,3.5,70);
                revealobjective(eob_objective6);
                Startup(GroupObjectID(43));
                
                trans sit;
            endif;
        endcase;
			
		endswitch;
	
endstate;

state lost;

		code
		
		switch cinema_cut
		
			case 0:	
			
			endcase;
			
			case 1:
			
			endcase;
			
		endswitch;
	
endstate;

state hang;
        code
endstate;

endfsm.
//******************************************************************

